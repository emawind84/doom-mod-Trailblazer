Actor SideSelected : Inventory { Inventory.MaxAmount 1 }
Actor SideUpgrade : Inventory { Inventory.MaxAmount 1 }

Actor LasersightPuff : BulletPuff
{
	+ALWAYSPUFF
	+PUFFGETSOWNER
	+BLOODLESSIMPACT
	+PUFFONACTORS
	+SKYEXPLODE
	+DONTSPLASH
	+NOTRIGGER
   	+NOTONAUTOMAP
	-ALLOWPARTICLES
	+NOTIMEFREEZE
	VSpeed 0
	Renderstyle Add
	Scale 0.1
	States
	{
		Spawn:
		Crash:
		Death:
		XDeath:
			RTIN A 2 Bright
			Stop
	}
}

Actor SidekickClip : Ammo
{
   Inventory.MaxAmount 16
   Inventory.Icon SDKGX0
   +IGNORESKILL
}

Actor SidekickEmptiedToken : Inventory { Inventory.MaxAmount 1 }
Actor SidekickZoomed : Inventory { Inventory.MaxAmount 1 }
Actor SidekickZoomReload : Inventory { Inventory.MaxAmount 1 }
Actor ZoomReFire : Inventory { Inventory.MaxAmount 1 }
Actor LazerActive : Inventory { Inventory.MaxAmount 1 }

Actor Sidekick : Weapon replaces Pistol
{
	Weapon.AmmoUse 1
	Weapon.AmmoGive 0
	Weapon.AmmoGive2 16
	Weapon.AmmoType "SidekickClip"
	Weapon.AmmoType2 "BulletAmmo"
	Weapon.SlotNumber 2
	Weapon.SelectionOrder 7
	Weapon.UpSound "Sidekick/Up"
	Weapon.Kickback 50
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Got yourself a Type 45. ''Sidekick'' combat pistol! Never get into fights without your trusty friend! (2)"
	Obituary "%k was assisted by ''Sidekick'' in the process of killing %o."
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.1
	Weapon.BobRangeY 0.3
	Weapon.BobRangeX 0.6
	+NOALERT
	//+NOAUTOFIRE
	+INVENTORY.UNDROPPABLE
	+AMMO_CHECKBOTH
	//+NOAUTOAIM
	States
	{
	Ready:
		SD2U A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKU A 0
		"####" A 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),1)
		"####" B 1
		"####" C 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),1)
		"####" D 1
		"####" E 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),1)
		"####" F 1
	ReadyMain:
		SD2G A 0 A_GiveInventory("LazerActive")
		SD2G A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKG A 0 A_TakeInventory("LazerActive")
		"####" A 0 A_JumpIfInventory("SidekickZoomed",1,"ZoomedReady")
		"####" A 0 A_SetCrosshair(0)
		"####" A 0 A_JumpIfInventory("MacheteGoesFast",1,"CaberToss")
		"####" A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	AltFire:
		SD2Z A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKZ A 0
		"####" A 0 A_PlaySound("Sights/Weapon",5,0.8)
		"####" A 0 A_JumpIfInventory("SidekickZoomed",1,"UnZoom")
		"####" A 0 A_ZoomFactor(2.5)
		"####" A 0 A_SetCrosshair(77)
		"####" ABCD 1
		SDKZ EF 1
		SDZG A 15 {
			A_GiveInventory("SidekickZoomed",1);
			A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD);
		}
		Goto ZoomedReady
	UnZoom:
		SDKZ FE 1
		SD2Z A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKZ A 0
		"####" A 0 A_ZoomFactor(1.0)
		"####" DCBA 1
		SD2G A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKG A 0
		"####" A 15 {
			A_SetCrosshair(0);
			A_TakeInventory("SidekickZoomed",1);
			A_WeaponReady(WRF_NOSECONDARY|WRF_ALLOWRELOAD|WRF_ALLOWZOOM);
		}
		Goto ReadyMain
	ZoomedReady:
		SDZG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
		Goto ReadyMain
	DryFire:
		"####" A 0 A_JumpIf(GetCvar("TB_ReloadMode"),"AutoReload")
		"####" A 0 A_PlaySound("Sidekick/Dry",4,0.6)
		SD2G A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKG A 0
		"####" A 1
		SD2T A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKT A 0
		"####" ABBA 1
		SD2G A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKG A 0
		"####" A 1
		Goto ReadyMain
	AutoReload:
		"####" A 0 A_JumpIfInventory("BulletAmmo",1,"Reload")
		Goto DryFire+1
	ZoomedDryFire:
		SDZG A 0 A_JumpIf(GetCvar("TB_ReloadMode"),"ZoomedAutoReload")
		SDZG A 0 A_PlaySound("Sidekick/Dry",4,0.8) //Louder in ironsights. DETAILS!
		SDZT ABCBA 1
		SDZG A 1
		Goto ReadyMain
	ZoomedAutoReload:
		"####" A 0 A_JumpIfInventory("BulletAmmo",1,"ZoomedReload")
		Goto ZoomedDryFire+1
	Deselect:
		"####" A 0 A_TakeInventory("SideSelected")
		"####" A 0 A_TakeInventory("PowerCrafterTimer")
		SD2U A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKU A 0
		"####" A 0 A_JumpIfInventory("SidekickZoomed",1,"ZoomedDeselect")
		"####" A 0 A_TakeInventory("LazerActive")
		"####" A 0 A_ZoomFactor(1.0)
		"####" A 0 A_SetCrosshair(0)
		"####" F 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),1)
		"####" E 1
		"####" D 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),1)
		"####" C 1
		"####" B 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),1)
		"####" A 1
		TNT1 A 0 SetPlayerProperty(0, 0, PROP_INSTANTWEAPONSWITCH)
		TNT1 AAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait
	ZoomedDeselect:
		SDKZ A 0 A_PlaySound("Sights/Weapon",5,0.8)
		SDKZ A 0 A_ZoomFactor(1.0)
		SDKZ A 0 A_SetCrosshair(0)
		SDKZ FEDCBA 1
		SD2G A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKG A 0
		"####" A 2 A_TakeInventory("SidekickZoomed",1)
		Goto Deselect
	Select:
		TNT1 A 0 A_GiveInventory("SideSelected")
		TNT1 A 0 SetPlayerProperty(0, 1, PROP_INSTANTWEAPONSWITCH)
	HongkaCzongka:
		TNT1 A 0 A_Raise
		TNT1 A 1 A_Raise
		Loop
	Fire:
		SD2F A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKF A 0
		"####" A 0 A_JumpIfInventory("SidekickZoomed",1,"ZoomedFire")
		"####" A 0 A_JumpIfInventory("SidekickClip",1,1)
		Goto DryFire
		
		"####" A 0 A_JumpIfInventory("SideUpgrade",1,"SilencedFire")
		"####" A 0 A_PlaySound("Sidekick/Fire",1,0.8)
		"####" A 0 A_PlaySound("Sidekick/Add",5,0.8)
		"####" A 0 A_FireCustomMissile("GunsmokeSpawner_Small",0,0,8,1,FPF_NOAUTOAIM)
		"####" A 0 A_AlertMonsters
		
		"####" A 0 A_JumpIfInventory("UraniumShot",1,2)
		"####" A 0 A_FireBullets(1.0, 0.25, 1, 10, "BulletPuff")
		Goto FireContinue
		"####" A 0 A_FireBullets(1.0, 0.25, 1, 10, "UraniumPuff")
		Goto FireContinue
	SilencedFire:
		"####" A 0 A_JumpIf(GetCvar("TB_LeftHanded"), "HAHAHA")
		
		"####" A 0 A_PlaySound("Sidekick/SilencedFire",1,0.8)
		"####" A 0 A_PlaySound("Sidekick/Add",5,0.3)
		"####" A 0 A_FireCustomMissile("GunsmokeSpawner_Small",0,0,2,7,FPF_NOAUTOAIM)
		
		"####" A 0 A_JumpIfInventory("UraniumShot",1,2)
		"####" A 0 A_FireBullets(0, 0, 1, 10, "BulletPuff")
		Goto FireContinue
		"####" A 0 A_FireBullets(0, 0, 1, 10, "UraniumPuff")
		Goto FireContinue	
	HAHAHA:
		"####" A 0 A_PlaySound("Sidekick/CommandoFire",1)
		//"####" A 0 A_PlaySound("Sidekick/Add",5,0.3)
		"####" A 0 A_FireCustomMissile("GunsmokeSpawner_Small",0,0,2,7,FPF_NOAUTOAIM)
		
		"####" A 0 A_JumpIfInventory("UraniumShot",1,2)
		"####" A 0 A_FireBullets(0, 0, 1, (200), "CommandoBulletPuff")
		Goto FireContinue
		"####" A 0 A_FireBullets(0, 0, 1, (200), "CommandoUraniumPuff")
		Goto FireContinue
		
	FireContinue:
		"####" A 0 {
				A_GunFlash;
				If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
				A_SetBlend("Blue",0.05,7);
			}
		}
		"####" A 1 Bright A_SetPitch(pitch-0.2 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" B 1 Bright A_SetPitch(pitch-0.1 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" C 1 A_JumpIf(Health <= 66 || CountInv("PowerBlazing"),1)
		"####" D 1 A_FireCustomMissile("BuellerCasingSpawner2",0,0,2,4,FPF_NOAUTOAIM) //I wanted to use spawnitemex, but then I realized it doesn't take player's pitch into account at all. I guess that flag is for projectiles, not player entities, sigh. Gonna leave it here just in case somebody will figure that one out. //A_SpawnItemEx("BuellerCasing",-12,21,30,0.1*frandom(-20,-30),0,0.1*frandom(5,15),random(90,100),SXF_TRANSFERPITCH,0)
		"####" E 1 A_JumpIf(Health <= 33 || CountInv("PowerBlazing"),1)
		SD2G A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKG A 0
		"####" A 1
		"####" A 0 A_ReFire
		Goto ReadyMain
	ZoomedFire:
		SDZF A 0 A_JumpIfInventory("SidekickClip",1,1)
		Goto ZoomedDryFire
		SDZF A 0 A_JumpIfInventory("SideUpgrade",1,"ZoomedSilencedFire")
		SDZF A 0 A_PlaySound("Sidekick/Fire",1,0.9)
		SDZF A 0 A_PlaySound("Sidekick/Add",5,0.9) //Louder in ironsights. DETAILS!
		SDZF A 0 A_AlertMonsters
		SDZF A 0 A_JumpIfInventory("UraniumShot",1,2)
		SDZF A 0 A_FireBullets(0.25, 0.0625, 1, 10, "BulletPuff")
		Goto ZoomedFireContinue
		SDZF A 0 A_FireBullets(0.25, 0.0625, 1, 10, "UraniumPuff")
		Goto ZoomedFireContinue
	ZoomedSilencedFire:
		SDZF A 0 A_JumpIf(GetCvar("TB_LeftHanded"), "REAL TOUGH GUY")
		
		SDZF A 0 A_PlaySound("Sidekick/SilencedFire",1,0.9)
		SDZF A 0 A_PlaySound("Sidekick/Add",5,0.3)
		SDZF A 0 A_FireCustomMissile("GunsmokeSpawner_Small",0,0,0,6,FPF_NOAUTOAIM)
		SDZF A 0 A_GunFlash
		SDZF A 0 A_JumpIfInventory("UraniumShot",1,2)
		SDZF A 0 A_FireBullets(0, 0, 1, 10, "BulletPuff")
		Goto ZoomedFireContinue
		SDZF A 0 A_FireBullets(0, 0, 1, 10, "UraniumPuff")
		Goto ZoomedFireContinue
	"REAL TOUGH GUY":
		SDZF A 0 A_PlaySound("Sidekick/CommandoFire",1)
		//SDZF A 0 A_PlaySound("Sidekick/Add",5,0.3)
		SDZF A 0 A_FireCustomMissile("GunsmokeSpawner_Small",0,0,0,6,FPF_NOAUTOAIM)
		SDZF A 0 A_GunFlash
		SDZF A 0 A_JumpIfInventory("UraniumShot",1,2)
		SDZF A 0 A_FireBullets(0, 0, 1, (200), "CommandoBulletPuff")
		Goto ZoomedFireContinue
		SDZF A 0 A_FireBullets(0, 0, 1, (200), "CommandoUraniumPuff")
		Goto ZoomedFireContinue
	ZoomedFireContinue:
		"####" A 0 {
				If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
				A_SetBlend("Blue",0.05,7);
			}
		}
		SDZF A 0 A_JumpIfInventory("SideUpgrade",1,6)
		SDZF A 1 Bright A_SetPitch(pitch-0.1 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		SDZF A 0 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(2.48); } }
		SDZF B 1 Bright A_SetPitch(pitch-0.1 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		SDZF A 0 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(2.49); } }
		SDZF A 0 A_Jump(256,5)
		SDZF E 1 A_SetPitch(pitch-0.1 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		SDZF A 0 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(2.48); } }
		SDZF D 1 A_SetPitch(pitch-0.1 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		SDZF A 0 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(2.49); } }
		
		SDZF C 1 A_JumpIf(Health <= 66 || CountInv("PowerBlazing"), 1)
		SDZF C 0 A_ZoomFactor(2.5)
		SDZF C 0 A_SetPitch(pitch+0.2 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		SDZF D 1 A_FireCustomMissile("BuellerCasingSpawner3",0,0,-2,8,FPF_NOAUTOAIM)
		SDZF E 1 A_JumpIf(Health <= 33 || CountInv("PowerBlazing"),1)
		SDZG A 1
		SDZG A 0 A_ReFire
		SDZG A 2
		Goto ReadyMain
	ZoomedReload:
		"####" A 0 A_PlaySound("Sights/Weapon",5,0.8)
		"####" A 0 A_ZoomFactor(1)
		SDKZ FE 1
		SD2Z A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKZ A 0
		"####" DCBA 1
		SD2G A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKG A 0
		"####" A 2 A_TakeInventory("SidekickZoomed",1)
		"####" A 0 A_GiveInventory("SidekickZoomReload",1)
	Reload:
		"####" A 0 A_JumpIfInventory("SidekickZoomed",1,"ZoomedReload")
		"####" A 0 A_JumpIfInventory("SidekickClip",0,"ReadyMain")
		"####" A 0 A_JumpIfInventory("HexenCounter",1,"HexenCheck")
	DoomCheck:
		"####" A 0 A_JumpIfInventory("BulletAmmo",1,1)
		Goto ReadyMain
		"####" A 0 A_JumpIfInventory("SidekickClip",1,"ReloadMain")
		Goto GetToDaChoppa
	HexenCheck:
		"####" A 0 A_JumpIfInventory("BlueMana",1,1)
		Goto ReadyMain
		"####" A 0 A_JumpIfInventory("SidekickClip",1,"ReloadMain")
	GetToDaChoppa:
		"####" A 0 A_GiveInventory("SidekickEmptiedToken",1)
	ReloadMain:
		"####" A 0 A_PlaySound("Sidekick/Out",6)
		"####" A 0 {
			if(GetCvar("TB_LeftHanded") && CountInv("SideUpgrade") && Random(1,2) == 1)
			{
				A_PlaySound("Sidekick/CommandoLoad",2);
			}
		}
		//SDKO ABCDEF 1
		//SDKO G 5 A_SpawnItemEx("SidekickClipDrop",0,0,20)
		/*SDKC ABCDEFGHIJKLMNOPQ 1
		SDKN ABCDEFGHIJ 1*/
		SD2N A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKN A 0
		"####" A 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),2)
		"####" BC 1
		"####" D 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" E 1
		"####" F 1 A_SpawnItemEx("SidekickClipDrop",0,0,20)
		"####" G 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" HI 1
		"####" J 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),2)
		"####" K 1
		
		SD2L A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKL A 0
		"####" A 0 A_PlaySound("Sidekick/In",6)
		"####" A 1
		"####" B 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" CD 1
		"####" E 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" F 1
	ReloadWorking:
		"####" A 0 A_JumpIfInventory("HexenCounter",1,"HexenReloadWorking")
		"####" A 0 A_TakeInventory("BulletAmmo",1,TIF_NOTAKEINFINITE)
		"####" A 0 A_GiveInventory("SidekickClip",1)
		"####" A 0 A_JumpIfInventory("SidekickClip",0,2)
		"####" A 0 A_JumpIfInventory("BulletAmmo",1,2)
		"####" A 0
		Goto ReloadFinish
		"####" A 0
		Goto ReloadWorking
	HexenReloadWorking:
		"####" A 0 A_TakeInventory("BlueMana",1,TIF_NOTAKEINFINITE)
		"####" A 0 A_GiveInventory("SidekickClip",1)
		"####" A 0 A_JumpIfInventory("SidekickClip",0,2)
		"####" A 0 A_JumpIfInventory("BlueMana",1,2)
		"####" A 0
		Goto ReloadFinish
		"####" A 0
		Goto HexenReloadWorking
	ReloadFinish:
		"####" FGHIJ 1
		"####" A 0 A_JumpIfInventory("SidekickEmptiedToken",1,"Kaczink")
		SD2G A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKG A 0
		"####" A 2
		"####" A 0 A_JumpIfInventory("SidekickZoomReload",1,"Derp")
		Goto ReadyMain
	Kaczink:
		SD2S A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKS A 0
		"####" A 0 A_TakeInventory("SidekickEmptiedToken",1)
		"####" A 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),2)
		"####" BC 1
		"####" D 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" EF 1
		"####" A 0 A_PlaySound("Sidekick/Cock",6)
		"####" G 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" HI 1
		"####" J 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),2)
		"####" KL 1
		"####" M 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" NO 1
		SD2G A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKG A 0
		"####" A 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),1)
		"####" A 1
		"####" A 0 A_JumpifInventory("SidekickZoomReload",1,"Derp")
		Goto ReadyMain
	Derp:
		"####" A 0 A_TakeInventory("SidekickZoomReload",1)
		"####" A 0 A_JumpIf(GetCvar("TB_SightsReturn"),"ReadyMain")
		Goto AltFire
	Zoom:
		"####" A 0 {
				If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
			}
		}
		QML1 A 0 A_Overlay(2,"PunchFlash")
		QML1 A 0 A_JumpIfInventory("PowerStrength2",1,2)
		QML1 A 0 A_Jump(256,3)
		FML1 A 0 A_SetBlend("Orange",0.05,10)
		FML1 A 0 A_PlaySound("Melee/Firepunch",5)
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),2)
		"####" A 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" B 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" C 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" D 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIfInventory("KineticFist",1,"KineticPunch")
		"####" A 0 A_JumpIfInventory("PowerStrength2",1,3)
		"####" A 0 A_CustomPunch(30, 1, 0, "PunchCracker", 100)
		"####" A 0 A_Jump(256,"PunchContinue")
		"####" A 0 A_CustomPunch(90, 1, 0, "FlamePunchCracker", 150,0.06)
		Goto PunchContinue
	KineticPunch:
		"####" A 0 A_JumpIfInventory("PowerStrength2",1,3)
		"####" A 0 A_CustomPunch(30, 1, 0, "PunchCracker", 300)
		"####" A 0 A_Jump(256,"PunchContinue")
		"####" A 0 A_CustomPunch(90, 1, 0, "FlamePunchCracker2", 350,0.06)
		Goto PunchContinue
	PunchContinue:
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" E 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" F 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),2)
		"####" G 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" H 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" I 1
		"####" J 1
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" K 1
		TNT1 A 5
		Goto ReadyMain
	Flash:
		TNT1 A 2 A_Light1
		TNT1 A 4 A_Light2
		TNT1 A 0 A_Light0
		Stop
	PunchFlash:
		SD2G A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKG A 0
		"####" A 1 A_PlaySound("Punch/Swing",6,1.0)
		SD2P A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKP A 0
		"####" ABC 1
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),2)
		"####" DE 1
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" FE 1
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" DC 1
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),2)
		"####" BA 1
		SD2G A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKG A 0
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" AA 1
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" AA 1
		Stop
	CaberToss:
		"####" A 1 A_JumpIfInventory("MacheteAmmo",1,1)
		Goto ReadyMain
		SD2U A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKU A 0
		"####" ECA 1
	CaberMain:
		TNT1 A 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),1)
		TNT1 A 1
		TNT1 A 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),1)
		TNT1 A 1
		TNT1 A 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),1)
		TNT1 A 1
		TNT1 A 0 {
				A_PlaySound("Machete1/Slash",1,0.9);
				A_PlaySound("Machete2/Slash",5,0.2);
				If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
			}
		}
		MCHT ABC 1
		TNT1 A 0 A_TakeInventory("MacheteAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("PowerStrength2",1,3)
		MCHT A 0 {
			if(A_JumpIf(CountInv("PowerAmmoJunkie") > 0, "Null")) {
				A_SpawnItemEx("MacheterangProjectileJunkie",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			} Else {
				A_SpawnItemEx("MacheterangProjectile",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			}
		}
		TNT1 A 0 A_Jump(256,2)
		MCHT A 0 {
			if(A_JumpIf(CountInv("PowerAmmoJunkie") > 0, "Null")) {
				A_SpawnItemEx("MacheterangBerserkProjectileJunkie",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			} Else {
				A_SpawnItemEx("MacheterangBerserkProjectile",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			}
		}
		// This solution is ten times better. Just let it bouncecount out.
		MCHT D 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing")  || CountInv("BlazingBooster"),1)
		MCHT E 1
		MCHT F 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),1)
		MCHT G 1
		MCHT H 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),1)
		MCHT I 1
		TNT1 A 3
		TNT1 A 0 A_JumpIf(CountInv("MacheteGoesFast") > 0 && CountInv("MacheteAmmo") > 0, "CaberMain")
		
		SD2U A 0 A_JumpIfInventory("SideUpgrade",1,2)
		SDKU A 0
		"####" BDF 1
		Goto ReadyMain
	Spawn:
		SDKG X -1
		Stop
	}
}
Actor ColtsSelected : Inventory { Inventory.MaxAmount 1 }
Actor ColtsUpgrade : Inventory { Inventory.MaxAmount 1 }

Actor CradleClip : Ammo
{
	Inventory.MaxAmount 6
	Inventory.Icon COLTX0 //COLTA0
	+IGNORESKILL
}

Actor GraveClip : Ammo
{
	Inventory.MaxAmount 6
	Inventory.Icon COLTB0
	+IGNORESKILL
}

Actor CradleBurst : Inventory { Inventory.MaxAmount 3 }
Actor GraveBurst : CradleBurst {}

Actor Colts : Weapon
{
	Weapon.AmmoUse 0
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoGive2 0
	Weapon.AmmoType "CradleClip"
	Weapon.AmmoType2 "GraveClip"
	Weapon.SlotNumber 2
	Weapon.SelectionOrder 2
	Weapon.UpSound "Sidekick/Up"
	Weapon.Kickback 125
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Obtained ''Cradle & Grave'' TMAR-armed break-action six shooters! Fastest hands on this side of the hell! (2)"
	Obituary "%o was shot in %p face's face by %k's ''Cradle & Grave'' pistols."  //Le reference to A. Call of Juarez, B. stupid youtube video. You name it.
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 2.2
	Weapon.BobRangeY 0.6
	Weapon.BobRangeX 0.8
	+NOALERT
	+WEAPON.AMMO_CHECKBOTH
	//+NOAUTOAIM
	States
	{
	Ready:
		CLTU ABC 1
		TNT1 A 0 A_Overlay(2,"RIdle")
		TNT1 A 0 A_Overlay(-3,"LIdle")
	ReadyMain:
		TNT1 A 1 A_WeaponReady
		Loop
	Fire:
		TNT1 A 0
		Goto ReadyMain
	LIdle:
		LCLG A 1
		LCLG A 0 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & (BT_ATTACK), "LFire")
		LCLG A 0 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & (BT_RELOAD), "LReload")
		LCLG A 0 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & (BT_ZOOM), "Punch")
		Loop
	Deselect:
		TNT1 A 0 {
			A_TakeInventory("ShotsWereFired");
			A_TakeInventory("ColtsSelected");
			A_TakeInventory("PowerCrafterTimer");
			SetPlayerProperty(0, 0, PROP_INSTANTWEAPONSWITCH);
			A_Overlay(2,"");
			A_Overlay(-3,"");
		}
		CLTU CBA 1
	DeselectMain:
		TNT1 AA 0 A_Lower	
		TNT1 A 1 A_Lower
		Loop
	Select:
		TNT1 A 0 A_GiveInventory("ColtsSelected")
		TNT1 A 0 SetPlayerProperty(0, 1, PROP_INSTANTWEAPONSWITCH)
	HonkaCzongka:
		TNT1 A 0 A_Raise
		TNT1 A 1 A_Raise
		Loop
	LFire:
		LCLG A 0 A_JumpIf(CountInv("GraveClip") || CountInv("ColtsUpgrade"), 1)
		Goto LReload
		LCLG A 0 { if(CountInv("ColtBurstshot")) { A_GiveInventory("GraveBurst",3); } }
	LGoing:
		LCLF A 0 {
			A_TakeInventory("GraveBurst",1);
			A_PlaySound("Colt/Fire",6,0.5);
			A_PlaySound("Colt/Add",5,0.8);
			A_AlertMonsters;
			A_Overlay(-2,"LGunflash");
			A_FireCustomMissile("GunsmokeSpawner_Small_Dark",0,0,-12,2,FPF_NOAUTOAIM);
			If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
				A_SetBlend("Blue",0.05,8);
			}
		}
		LCLF A 0 {
			If (A_JumpIf(CountInv("ColtsUpgrade") > 0,"Null"))
			{
				A_FireBullets(1.5, 1.0, -1, 7, "BulletPuff");
			}
			Else
			{
				A_TakeInventory("GraveClip",1,TIF_NOTAKEINFINITE);
				A_FireBullets(1.5, 1.0, -1, 6, "BulletPuff");
			}
		}
		LCLF A 1 Bright {
			If (A_JumpIf(CountInv("ColtsUpgrade") > 0,"Null")) { Return State(""); }
			Else { A_SetPitch(pitch-0.75 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE); Return State(""); } }
		LCLF B 1 Bright {
			If (A_JumpIf(CountInv("ColtsUpgrade") > 0,"Null")) { Return State(""); }
			Else { A_SetPitch(pitch-0.25 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE); Return State(""); } }
		LCLF C 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),1)
		LCLF D 1
		LCLF D 0 { if(GetCvar("TB_SequentialShot") && CountInv("GraveBurst")) {
				A_GiveInventory("ShotsWereFired",1);
			}
		}
		LCLF A 0 A_JumpIf(CountInv("GraveClip") && CountInv("GraveBurst"), "LGoing")
		LCLF E 1
		LCLF F 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),1)
		LCLF F 0 { if(GetCvar("TB_SequentialShot")) {
				A_GiveInventory("ShotsWereFired",1);
			}
		}
		LCLF GH 1
		LCLG A 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),1)
		LCLG A 0 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & (BT_ATTACK), "LFire")
		LCLG A 0 A_TakeInventory("ShotsWereFired",1)
		Goto LIdle
	LGunflash:
		TNT1 A 4 A_Light2
		TNT1 A 6 A_Light1
		TNT1 A 0 A_Light0
		Stop
	LNope: //Not really used, but left just in case.
		LCLN A 0 A_PlaySound("Sidekick/Dry",4,0.7)
		LCLN A 0 A_TakeInventory("IsReloading",1)
		LCLN ABC 1
		LCLG A 8
		Goto LIdle
	LReload:
		//LCLG A 0 A_JumpIfInventory("Clip",1,1)
		//Goto LNope
		LCLG A 0 A_JumpIfInventory("GraveClip",0,"LIdle")
		LCLR A 0 A_PlaySound("Colt/Open",4,0.8)
		LCLR AB 1
		LCLR C 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		LCLR DE 1
		LCLR F 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		LCLR GG 1 //A_FireCustomMissile("ColtCasingSpawnerLeft",0,0,-2,0)
	LCasings:
		LCLR G 0 A_JumpIfInventory("GraveClip",0,"LEnd")
		LCLR G 0 A_GiveInventory("GraveClip",1)
		LCLR G 0 A_FireCustomMissile("ColtCasingSpawnerLeft",0,0,-2,0,FPF_NOAUTOAIM)
		Goto LCasings
		//LCLR G 2
	//LStart:
		//TNT1 A 0 A_JumpIfInventory("GraveClip",0,"LEnd")
		//TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		//Goto LEnd
		//TNT1 A 0 A_TakeInventory("Clip",1,1)
		//TNT1 A 0 A_GiveInventory("GraveClip",6)
		//goto LStart
	LEnd:
		LCLR HI 1
		LCLR J 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		LCLR KL 1
		LCLR M 0 A_PlaySound("Colt/Close",4,0.8)
		LCLR MN 1
		LCLR O 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"), 2)
		LCLR PQ 1
		LCLR R 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		LCLR SS 1
		LCLR R 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		LCLR QPON 1
		Goto LIdle
	Punch:
		CLT2 CBA 1
	PunchOutlaws:
		"####" A 0 {
				If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
			}
		}
		QML1 F 0 A_PlaySound("Punch/Swing",6,1.0)
		QML1 A 0 A_JumpIfInventory("PowerStrength2",1,2)
		QML1 A 0 A_Jump(256,3)
		FML1 A 0 A_SetBlend("Orange",0.05,10)
		FML1 A 0 A_PlaySound("Melee/Firepunch",5)
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" A 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" B 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" C 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" D 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIfInventory("KineticFist",1,"KineticPunch")
		"####" A 0 A_JumpIfInventory("PowerStrength2",1,3)
		"####" A 0 A_CustomPunch(30, 1, 0, "PunchCracker", 100)
		"####" A 0 A_Jump(256,"PunchContinue")
		"####" A 0 A_CustomPunch(90, 1, 0, "FlamePunchCracker", 150,0.06)
		Goto PunchContinue
	KineticPunch:
		"####" A 0 A_JumpIfInventory("PowerStrength2",1,3)
		"####" A 0 A_CustomPunch(30, 1, 0, "PunchCracker", 300)
		"####" A 0 A_Jump(256,"PunchContinue")
		"####" A 0 A_CustomPunch(90, 1, 0, "FlamePunchCracker2", 350,0.06)
		Goto PunchContinue
	PunchContinue:
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" E 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" F 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" G 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" H 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" I 1
		"####" J 1
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" K 1
		TNT1 A 5
		TNT1 A 0 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & (BT_ZOOM), "PunchOutlaws")
		CLT2 ABC 1
		Goto LIdle
		
	RIdle:
		RCLG A 1
		RCLG A 0 {
			If(CountInv("ShotsWereFired") && GetPlayerInput(INPUT_BUTTONS) & (BT_ATTACK)) { Return State("RFire"); }
			Else If(GetPlayerInput(INPUT_BUTTONS) & (BT_ALTATTACK)) { Return State("RFire"); }
			Return State("");
		}
		RCLG A 0 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & (BT_RELOAD), "RReload")
		RCLG A 0 A_JumpIf(CountInv("MacheteGoesFast") > 0 && CountInv("MacheteAmmo") > 0, "CaberToss")
		Loop
	RFire:
		RCLG A 0 A_JumpIf(CountInv("CradleClip") || CountInv("ColtsUpgrade"), 1)
		Goto RReload
		RCLG A 0 { if(CountInv("ColtBurstshot")) { A_GiveInventory("CradleBurst",3); } }
	RGoing:
		RCLF A 0 {
			A_TakeInventory("CradleBurst",1);
			A_PlaySound("Colt/Fire",1,0.5);
			A_PlaySound("Colt/Add",5,0.8);
			A_Overlay(3,"RGunflash");
			A_FireCustomMissile("GunsmokeSpawner_Small_Dark",0,0,12,2,FPF_NOAUTOAIM);
			A_AlertMonsters;
			If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
				A_SetBlend("Blue",0.05,8);
			}
		}
		RCLF A 0 {
			If (A_JumpIf(CountInv("ColtsUpgrade") > 0,"Null"))
			{
				A_FireBullets(1.5, 1.0, -1, 7, "BulletPuff");
			}
			Else
			{
				A_TakeInventory("CradleClip",1,TIF_NOTAKEINFINITE);
				A_FireBullets(1.5, 1.0, -1, 6, "BulletPuff");
			}
		}
		RCLF A 1 Bright {
			If (A_JumpIf(CountInv("ColtsUpgrade") > 0,"Null")) { Return State(""); }
			Else { A_SetPitch(pitch-0.75 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE); Return State(""); } }
		RCLF B 1 Bright {
			If (A_JumpIf(CountInv("ColtsUpgrade") > 0,"Null")) { Return State(""); }
			Else { A_SetPitch(pitch-0.25 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE); Return State(""); } }
		RCLF C 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),1)
		RCLF D 1
		RCLF A 0 A_JumpIf(CountInv("CradleClip") && CountInv("CradleBurst"), "RGoing")
		RCLF E 1
		RCLF F 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),1)
		RCLF GH 1
		RCLG A 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),1)
		RCLG A 0 {
			If(CountInv("ShotsWereFired") && GetPlayerInput(INPUT_BUTTONS) & (BT_ATTACK)) { Return State("RFire"); }
			Else If(GetPlayerInput(INPUT_BUTTONS) & (BT_ALTATTACK)) { Return State("RFire"); }
			Return State("");
		}
		Goto RIdle
	RGunflash:
		TNT1 A 4 A_Light2
		TNT1 A 6 A_Light1
		TNT1 A 0 A_Light0
		Stop
	RNope:
		RCLN A 0 A_PlaySound("Sidekick/Dry",4,0.7)
		RCLN A 0 A_TakeInventory("IsReloading",1)
		RCLN ABC 1
		RCLG A 8
		Goto RIdle
	RReload:
		//RCLG A 0 A_JumpIfInventory("Clip",1,1)
		//Goto RNope
		LCLG A 0 A_JumpIfInventory("CradleClip",0,"RIdle")
		RCLR A 0 A_PlaySound("Colt/Open",4,0.8)
		RCLR AB 1
		RCLR C 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		RCLR DE 1
		RCLR F 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		RCLR GG 1 //A_FireCustomMissile("ColtCasingSpawnerRight",0,0,-2,0)
	RCasings:
		RCLR G 0 A_JumpIfInventory("CradleClip",0,"REnd")
		RCLR G 0 A_GiveInventory("CradleClip",1)
		RCLR G 0 A_FireCustomMissile("ColtCasingSpawnerRight",0,0,0,0,FPF_NOAUTOAIM)
		Goto RCasings
		//RCLR G 2
	//RStart:
		//TNT1 A 0 A_JumpIfInventory("CradleClip",0,"REnd")
		//TNT1 A 0 A_JumpIfInventory("Clip",1,1)
		//Goto REnd
		//TNT1 A 0 A_TakeInventory("Clip",1,1)
		//TNT1 A 0 A_GiveInventory("CradleClip",6)
		//goto RStart
	REnd:
		RCLR HI 1
		RCLR J 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		RCLR KL 1
		RCLR M 0 A_PlaySound("Colt/Close",4,0.8)
		RCLR MN 1
		RCLR O 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		RCLR PQ 1
		RCLR R 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		RCLR SS 1
		RCLR R 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		RCLR QPON 1
		RCLG A 2 A_TakeInventory("IsReloading",1)
		Goto RIdle
	CaberToss:
		CLT1 CBA 1
	CaberMain:
		TNT1 A 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),1)
		TNT1 A 1
		TNT1 A 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),1)
		TNT1 A 1
		TNT1 A 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),1)
		TNT1 A 1
		TNT1 A 0 {
				A_PlaySound("Machete1/Slash",1,0.9);
				A_PlaySound("Machete2/Slash",5,0.2);
				If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
			}
		}
		MCHT ABC 1
		TNT1 A 0 A_TakeInventory("MacheteAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("PowerStrength2",1,3)
		MCHT A 0 {
			if(A_JumpIf(CountInv("PowerAmmoJunkie") > 0, "Null")) {
				A_SpawnItemEx("MacheterangProjectileJunkie",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			} Else {
				A_SpawnItemEx("MacheterangProjectile",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			}
		}
		TNT1 A 0 A_Jump(256,2)
		MCHT A 0 {
			if(A_JumpIf(CountInv("PowerAmmoJunkie") > 0, "Null")) {
				A_SpawnItemEx("MacheterangBerserkProjectileJunkie",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			} Else {
				A_SpawnItemEx("MacheterangBerserkProjectile",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			}
		}
		// This solution is ten times better. Just let it bouncecount out.
		MCHT D 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),1)
		MCHT E 1
		MCHT F 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),1)
		MCHT G 1
		MCHT H 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),1)
		MCHT I 1
		TNT1 A 3
		TNT1 A 0 A_JumpIf(CountInv("MacheteGoesFast") > 0 && CountInv("MacheteAmmo") > 0, "CaberMain")

		CLT1 ABC 1
		Goto RIdle
	Spawn:
		COLT X -1
		Stop
	}
}
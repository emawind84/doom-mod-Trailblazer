Actor ChromeSelected : Inventory { Inventory.MaxAmount 1 }
Actor ChromeUpgrade  : Inventory { Inventory.MaxAmount 1 }

Actor ChromeJusticeClip : Ammo
{
	Inventory.MaxAmount 10
	Inventory.Icon PCFGX0
	+IGNORESKILL
}

Actor DryToken : Inventory { Inventory.MaxAmount 1 }
Actor IsReloading : Inventory { Inventory.MaxAmount 1 }
Actor IsReloading2 : Inventory { Inventory.MaxAmount 1 }

Actor ChromeJustice : Weapon //replaces SuperShotgun //Random spawner-replaced.
{
	Weapon.AmmoUse 1
	Weapon.AmmoUse2 0
	Weapon.AmmoGive 0
	Weapon.AmmoGive2 8
	Weapon.AmmoType "ChromeJusticeClip"
	Weapon.AmmoType2 "DemShellsAmmo"
	Weapon.SlotNumber 3
	Weapon.SelectionOrder 3
	Weapon.UpSound "ChromeJustice/Up"
	Weapon.Kickback 150
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Got yourself a ''Chrome Justice'' shotgun! Preserving law'n'order, now with 140% more fire! (3)"
	Obituary "%o had to remain calm, and even dead, enforced by %k's ''Chrome Justice''."
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 1.8
	Weapon.BobRangeY 0.4
	Weapon.BobRangeX 0.8
	+AMMO_CHECKBOTH
	+NOALERT
	+NOAUTOFIRE
	States
	{
	Ready:
		SPSP A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFP A 0
		"####" I 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" HG 1
		"####" F 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" ED 1
		"####" C 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" BA 1
	ReadyMain:
		SPSG A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFG A 0
		"####" A 0 A_JumpIf(CountInv("MacheteGoesFast") > 0 && CountInv("MacheteAmmo") > 0, "CaberToss")
		"####" A 1 A_WeaponReady(WRF_ALLOWRELOAD|WRF_ALLOWZOOM)
		Loop
	DryFire:
		"####" A 0 A_JumpIf(GetCvar("TB_ReloadMode"),"AutoReload")
		SPST A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFT A 0
		"####" A 0 A_PlaySound("ChromeJustice/Dry",6,0.7)
		"####" ABCDCBA 1
		SPSG A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFG A 0
		"####" A 6
		Goto ReadyMain
	AutoReload:
		"####" A 0 A_JumpIfInventory("DemShellsAmmo",1,"Reload")
		Goto DryFire+1
	Deselect:
		"####" A 0 {
			A_TakeInventory("ChromeSelected");
			A_TakeInventory("PowerCrafterTimer");
			A_TakeInventory("IsReloading");
			A_TakeInventory("ZoomReFire");
		}
		SPSG A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFG A 0
		"####" AA 0 A_Lower
		"####" A 1 A_Lower
		Loop
	Select:
		"####" A 0 A_GiveInventory("ChromeSelected")
		SPSP A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFP A 0
		"####" II 0 A_Raise
		"####" I 1 A_Raise
		Loop
	Fire:
		"####" A 0 A_JumpIf(GetCvar("TB_JusticeSwap"), "AltFireSwapped")
	FireSwapped:
		"####" A 0 A_JumpIfInventory("ChromeUpgrade",1,"UpgFire") //Since the difference between regular and auto-fire is huge, I might as well make separate state for that.
		PC2F A 0 A_JumpIfInventory("IsReloading",1,"ReloadFinish")
		PC2F A 0 A_JumpIfInventory("ChromeJusticeClip",1,1)
		Goto DryFire
		PC2F A 0 {
			A_PlaySound("ChromeJustice/Fire",1,0.9);
			A_PlaySound("ChromeJustice/Alt",5,0.75);
			A_FireCustomMissile("GunsmokeSpawner_Medium_Dark",0,0,10,4,FPF_NOAUTOAIM);
			A_TakeInventory("ChromeJusticeClip",1,TIF_NOTAKEINFINITE);
			A_AlertMonsters;
			A_GunFlash;
			A_Recoil(6.0);
			If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
				A_SetBlend("Blue",0.15,15);
			}
		}
		PC2F AAAAAAA 0 A_FireCustomMissile("ChromeJusticeSonicfire",frandom(6.0,-6.0),0,11,4,FPF_NOAUTOAIM,frandom(2.0,-2.0))

		PC2F A 1 Bright {
			if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.93); }
			A_SetPitch(pitch-2.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		PC2F B 1 Bright{
			if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.95); }
			A_SetPitch(pitch-1.75 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		PC2P A 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.955); } }
		TNT1 A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		PC2P B 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.96); } }
		PC2P C 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.97); } }
		TNT1 A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		PC2P D 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.98); } }
		PC2P E 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.99); } }
		TNT1 A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		PC2P F 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.995); } }
		PC2P G 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(1.0); } }
		PC2P HI 1
		PC2P A 0 A_PlaySound("ChromeJustice/Pump",6,0.6)
		PC2P J 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		PC2P K 1
		PCFP L 1
		PCFP M 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		PCFP M 1 //A_FireCustomMissile("ShottyCasingSpawner",0,0,0,0)
		PCFP L 1 A_FireCustomMissile("DatShottyCasingSpawner",0,0,0,0,FPF_NOAUTOAIM)
		PCFP K 1 //A_FireCustomMissile("ShottyCasingSpawner",0,0,0,0) //Hurrgineer was here.
		PCFP J 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		PCFP IHG 1
		PCFP F 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		PCFP EDC 1
		PCFP B 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		PCFP A 1
		PCFT AB 1
		PCFT C 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		PCFT D 1
		PCFG A 1
		PCFG A 0 A_ReFire
		Goto ReadyMain
	UpgFire:
		SPSG A 0 A_JumpIfInventory("IsReloading",1,"ReloadFinish")
		SPSG A 0 A_JumpIfInventory("ChromeJusticeClip",1,1)
		Goto DryFire
		SPSF A 0 {
			A_PlaySound("AutoJustice/Fire",1,0.5);
			A_PlaySound("ChromeJustice/Alt",5,0.75);
			A_FireCustomMissile("GunsmokeSpawner_Medium_Dark",0,0,10,4,FPF_NOAUTOAIM);
			A_TakeInventory("ChromeJusticeClip",1,TIF_NOTAKEINFINITE);
			A_AlertMonsters;
			A_GunFlash;
			A_Recoil(6.0);
			If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
				A_SetBlend("Blue",0.15,15);
			}
		}
		//PC2F A 0 A_TakeInventory("ChromeJusticeClip",3,TIF_NOTAKEINFINITE)
		SPSF AAAAAAA 0 A_FireCustomMissile("ChromeJusticeSonicfire",frandom(6.0,-6.0),0,11,4,FPF_NOAUTOAIM,frandom(2.0,-2.0))

		SPSF C 1 Bright {
			if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.93); }
			A_SetPitch(pitch-2.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		SPSF D 1 Bright {
			if(GetCvar("TB_ZoomRecoil")) {  A_ZoomFactor(0.95); }
			A_SetPitch(pitch-1.75 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		
		TNT1 A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		SPSG B 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.96); } }
		SPSG C 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.97); } }

		TNT1 A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		SPSG D 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.98); } }
		SPSG E 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.99); } }
		
		SPSG E 0 A_FireCustomMissile("DatShottyCasingSpawner",0,0,0,0,FPF_NOAUTOAIM)

		TNT1 A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		SPSG F 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.995); } }
		SPSG G 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(1.0); } }
		SPSG HI 1 A_Weaponready

		TNT1 A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		SPSG J 1 A_Weaponready
		SPSG K 1 A_Weaponready
		SPSG A 1 A_Weaponready
		SPSG A 0 A_ReFire("UpgFire")
		Goto ReadyMain
	
	AltFire:
		"####" A 0 A_JumpIf(GetCvar("TB_JusticeSwap"), "FireSwapped")
	AltFireSwapped:
		"####" A 0 A_JumpIfInventory("ChromeUpgrade",1,"UpgAltFire")
		"####" A 0 A_JumpIfInventory("IsReloading",1,"ReloadFinish")
		"####" A 0 A_JumpIfInventory("ChromeJusticeClip",1,1)
		Goto DryFire
		PCFF A 0 {
			A_PlaySound("ChromeJustice/Fire",1);
			A_FireCustomMissile("GunsmokeSpawner_Medium",0,0,10,4,FPF_NOAUTOAIM);
			A_FireBullets(2.2, 2.2, 25, 5, "BulletPuff",0);
			A_TakeInventory("ChromeJusticeClip",1,TIF_NOTAKEINFINITE);
			A_AlertMonsters;
			A_GunFlash;
			A_Recoil(1.0);
			If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
				A_SetBlend("Blue",0.15,15);
			}
		}
		PCFF A 1 Bright {
			if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.975); }
			A_SetPitch(pitch-1.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		PCFF B 1 Bright {
			if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.98); }
			A_SetPitch(pitch-0.75 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		PCFP A 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.985); } }
		TNT1 A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		PCFP B 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.99); } }
		PCFP C 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.9925); } }
		TNT1 A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		PCFP D 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.995); } }
		PCFP E 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(1.0); } }
		PCFP F 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		PCFP GHI 1
		PCFP A 0 A_PlaySound("ChromeJustice/Pump",6,0.6)

		PCFP J 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		PCFP KL 1
		PCFP M 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		PCFP M 1
		PCFP L 1 A_FireCustomMissile("DatShottyCasingSpawner",0,0,0,0)
		PCFP K 1
		PCFP J 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		PCFP IHG 1
		PCFP F 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		PCFP EDC 1
		PCFP B 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		PCFP A 1
		PCFT AB 1
		PCFT C 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		PCFT D 1
		PCFG A 1
		PCFG A 0 A_ReFire
		Goto ReadyMain
	UpgAltFire:
		SPSG A 0 A_JumpIfInventory("IsReloading",1,"ReloadFinish")
		SPSG A 0 A_JumpIfInventory("ChromeJusticeClip",1,1)
		Goto DryFire
		SPSG A 0 {
			A_PlaySound("AutoJustice/Fire",1,0.5);
			A_FireCustomMissile("GunsmokeSpawner_Medium",0,0,10,4,FPF_NOAUTOAIM);
			A_FireBullets(2.2, 2.2, 25, 5, "BulletPuff",0);
			A_TakeInventory("ChromeJusticeClip",1,TIF_NOTAKEINFINITE);
			A_AlertMonsters;
			A_GunFlash;
			A_Recoil(1.0);
			If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
				A_SetBlend("Blue",0.15,15);
			}
		}
		SPSF A 1 Bright { 
			if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.975); }
			A_SetPitch(pitch-1.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		SPSF B 1 Bright { 
			if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.98); }
			A_SetPitch(pitch-0.75 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		
		TNT1 A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		SPSG B 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.96); } }
		SPSG C 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.97); } }

		TNT1 A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		SPSG D 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.98); } }
		SPSG E 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.99); } }
		
		SPSG E 0 A_FireCustomMissile("DatShottyCasingSpawner",0,0,0,0,FPF_NOAUTOAIM)

		TNT1 A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		SPSG F 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(0.995); } }
		SPSG G 1 { if(GetCvar("TB_ZoomRecoil")) { A_ZoomFactor(1.0); } }
		SPSG HI 1 A_Weaponready

		TNT1 A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		SPSG J 1 A_Weaponready
		SPSG K 1 A_Weaponready
		SPSG A 1 A_Weaponready
		SPSG A 0 A_ReFire("UpgAltFire")
		Goto ReadyMain
	DriedToBoot:
		"####" A 0 A_GiveInventory("DryToken",1)
		Goto ReloadMainie
	Reload:
		"####" A 0 A_JumpIfInventory("IsReloading2",1,"Reload2")
		"####" A 0 A_JumpIfInventory("ChromeJusticeClip",0,"ReadyMain")
		"####" A 0 A_JumpIfInventory("HexenCounter",1,2)
		"####" A 0 A_JumpIfInventory("DemShellsAmmo",1,2)
		Goto ReadyMain
		"####" A 0 A_JumpIfInventory("GreenMana",8,1)
		Goto ReadyMain
	ReloadMain:
	   "####" A 0 A_JumpIfInventory("ChromeJusticeClip",1,1)
		Goto DriedToBoot
	ReloadMainie:
		SPSP A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFP A 0
		"####" A 0 A_GiveInventory("IsReloading",1)
		"####" A 1
		"####" B 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" CDEF 1
		"####" G 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" HI 1
		SPSV A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFH A 0
		"####" CE 1
		"####" G 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" HJK 1
		"####" L 1
		"####" A 0 A_JumpIfInventory("JusticeStriploader", 1, "Stripload")
	ReloadWorking:
		SPSR A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFR A 0
		"####" A 0 A_JumpIfInventory("HexenCounter",1,"HexenReloadWorking")
		"####" A 0 A_PlaySound("ChromeJustice/Load",6)
		"####" A 0 A_GiveInventory("ZoomReFire",1)
		"####" E 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" FHI 1
		"####" A 0 A_TakeInventory("DemShellsAmmo",1,TIF_NOTAKEINFINITE)
		"####" K 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" LM 1
		"####" A 0 A_GiveInventory("ChromeJusticeClip",1)
		SPSV A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFH A 0
		"####" L 3 A_WeaponReady(WRF_NOBOB/*|WRF_NOSWITCH*/|WRF_ALLOWZOOM)
		"####" A 0 A_TakeInventory("ZoomReFire",1)
		"####" A 0 A_JumpIfInventory("ChromeJusticeClip",0,"ReloadFinish")
		"####" A 0 A_JumpIfInventory("DemShellsAmmo",1,"ReloadWorking")
		Goto ReloadFinish
	Reload2:
		"####" A 0 A_JumpIfInventory("HexenCounter",1,"HexenReload2")
		"####" A 0 A_TakeInventory("IsReloading2",1)
		"####" A 0 A_JumpIfInventory("ChromeJusticeClip",0,"ReloadFinish")
		"####" A 0 A_JumpIfInventory("JusticeStriploader",1,"Stripload")
		"####" A 0 A_JumpIfInventory("DemShellsAmmo",1,"ReloadWorking")
		Goto ReloadFinish
	HexenReloadWorking:
		"####" A 0 A_PlaySound("ChromeJustice/Load",6)
		"####" A 0 A_GiveInventory("ZoomReFire",1)
		"####" E 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" FHI 1
		"####" A 0 A_TakeInventory("GreenMana",8,TIF_NOTAKEINFINITE)
		"####" K 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" LM 1
		"####" A 0 A_GiveInventory("ChromeJusticeClip",1)
		SPSV A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFH A 0
		"####" L 3 A_WeaponReady(WRF_NOBOB/*|WRF_NOSWITCH*/|WRF_ALLOWZOOM)
		"####" A 0 A_TakeInventory("ZoomReFire",1)
		"####" A 0 A_JumpIfInventory("ChromeJusticeClip",0,"ReloadFinish")
		"####" A 0 A_JumpIfInventory("GreenMana",8,"ReloadWorking")
		Goto ReloadFinish
	HexenReload2:
		"####" A 0 A_TakeInventory("IsReloading2",1)
		"####" A 0 A_JumpIfInventory("ChromeJusticeClip",0,"ReloadFinish")
		"####" A 0 A_JumpIfInventory("JusticeStriploader",1,"Stripload")
		"####" A 0 A_JumpIfInventory("GreenMana",8,"ReloadWorking")
		Goto ReloadFinish
	ReloadFinish:
		SPSV A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFH A 0
		"####" A 0 A_TakeInventory("ZoomReFire",1)
		"####" L 1 A_TakeInventory("IsReloading",1)
		"####" L 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),3)
		"####" LKJH 1
		"####" G 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" EC 1
		"####" A 0 A_JumpIfInventory("DryToken",1,"Kaczink")
	GoingBackToMacDonalds:
		SPSP A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFP A 0
		"####" IHG 1
		"####" F 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" EDC 1
		"####" B 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" A 1
		"####" A 0 A_JumpIfInventory("ChromeUpgrade",1,"GoingAutoToMacDonalds")
		PCFT B 1
		PCFT C 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		PCFT D 1
		PCFG A 2 A_ReFire
		Goto ReadyMain
	GoingAutoToMacDonalds:
		SPSG A 2 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		SPSG A 2
		SPSG A 0 A_ReFire
		Goto ReadyMain
	Kaczink:
		PCFP A 0 A_TakeInventory("DryToken",1)
		PCFP A 0 A_JumpIfInventory("ChromeUpgrade",1,"GoingBackToMacDonalds")
		PCFP A 0 A_PlaySound("ChromeJustice/Pump",6,0.6)
		PCFP J 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		PCFP KL 1
		PCFP M 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		PCFP MLKJ 1
		Goto GoingBackToMacDonalds
	Stripload:
		SPSS A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFS A 0
		"####" A 0 A_PlaySound("Shells/Ammo",6)
		"####" A 0 A_GiveInventory("ZoomReFire",1)
		"####" A 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" CE 1
		"####" A 0 A_GiveInventory("ZoomReFire",1)
		"####" E 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" FHI 1
	StriploadWorking:
		"####" A 0 A_JumpIfInventory("HexenCounter",1,"HexenStriploadWorking")
		"####" A 0 A_TakeInventory("DemShellsAmmo",1,TIF_NOTAKEINFINITE)
		"####" A 0 A_GiveInventory("ChromeJusticeClip", 1)
		"####" A 0 A_JumpIfInventory("ChromeJusticeClip",0,2)
		"####" A 0 A_JumpIfInventory("DemShellsAmmo",1,2)
		"####" A 0
		Goto StriploadFinish
		"####" A 0
		Goto StriploadWorking
	HexenStriploadWorking:
		"####" A 0 A_TakeInventory("GreenMana",8,TIF_NOTAKEINFINITE)
		"####" A 0 A_GiveInventory("ChromeJusticeClip", 1)
		"####" A 0 A_JumpIfInventory("ChromeJusticeClip",0,2)
		"####" A 0 A_JumpIfInventory("GreenMana",8,2)
		"####" A 0
		Goto StriploadFinish
		"####" A 0
		Goto HexenStriploadWorking
	StriploadFinish:
		SPSR A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFR A 0
		"####" K 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" LM 1
		SPSV A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFH A 0
		"####" L 3 A_WeaponReady(WRF_NOBOB/*|WRF_NOSWITCH*/|WRF_ALLOWZOOM)
		"####" A 0 A_TakeInventory("ZoomReFire",1)
		Goto ReloadFinish
	Flash:
		TNT1 A 6 A_Light2
		TNT1 A 8 A_Light1
		TNT1 A 0 A_Light0
		Stop
	AltFlash:
		TNT1 A 3 A_Light2
		TNT1 A 6 A_Light1
		TNT1 A 0 A_Light0
		Stop
	
	Zoom:
		SPSP A 0 A_JumpIfInventory("ZoomReFire",1,"PunchCriminalScums")
		SPSP A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFP A 0
		"####" ADFI 1
		SPSV A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFH A 0
		"####" EJK 1
		"####" A 0 A_GiveInventory("ZoomReFire",1)
	PunchCriminalScums:
		"####" A 0 {
				If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
			}
		}
		QML1 A 0 A_Overlay(2,"PunchFlash")
		QML1 A 0 A_JumpIfInventory("PowerStrength2",1,2)
		QML1 A 0 A_Jump(256,3)
		FML1 A 0 A_SetBlend("Orange",0.05,10)
		FML1 A 0 A_PlaySound("Melee/Firepunch",5)
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" A 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" B 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" C 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" D 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIfInventory("KineticFist",1,"KineticPunch")
		"####" A 0 A_JumpIfInventory("PowerStrength2",1,3)
		"####" A 0 A_CustomPunch(30, 1, 0, "PunchCracker", 100)
		"####" A 0 A_Jump(256,"PunchContinue")
		"####" A 0 A_CustomPunch(90, 1, 0, "FlamePunchCracker", 150,0.06)
		Goto PunchContinue
	KineticPunch:
		"####" A 0 A_JumpIfInventory("PowerStrength2",1,3)
		"####" A 0 A_CustomPunch(30, 1, 0, "PunchCracker", 300)
		"####" A 0 A_Jump(256,"PunchContinue")
		"####" A 0 A_CustomPunch(90, 1, 0, "FlamePunchCracker2", 350,0.06)
		Goto PunchContinue
	PunchContinue:
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" E 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" F 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" G 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" H 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" I 1
		"####" J 1
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" K 1
		TNT1 A 3 A_GiveInventory("IsReloading2",1)
		TNT1 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_ALLOWRELOAD|WRF_NOBOB)
		TNT1 A 1 A_TakeInventory("IsReloading2",1)
		TNT1 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_ALLOWZOOM|WRF_ALLOWRELOAD)
		SPSV A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFH A 0
		"####" KJC 1
		SPSP A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFP A 0
		"####" IDCA 1
		"####" A 0 A_TakeInventory("IsReloading",1)
		"####" A 0 A_TakeInventory("ZoomReFire",1)
		Goto ReadyMain
	PunchFlash:
		SPSV A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFH A 0
		"####" L 1 A_PlaySound("Punch/Swing",6,1.0)
		SPSM A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFM A 0
		"####" ABC 1
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" BA 1
		SPSV A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFH A 0
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" LL 1
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" LL 1
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" LL 1
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),2)
		"####" LL 1
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),2)
		"####" LL 1
		Stop
	CaberToss:
		"####" A 0 A_JumpIfInventory("MacheteAmmo",1,1)
		Goto ReadyMain
		SPSN A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFN A 0
		"####" CBA 1
	CaberMain:
		TNT1 A 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),1)
		TNT1 A 1
		TNT1 A 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),1)
		TNT1 A 1
		TNT1 A 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),1)
		TNT1 A 1
		TNT1 A 0 {
				A_PlaySound("Machete1/Slash",1,0.9);
				A_PlaySound("Machete2/Slash",5,0.2);
				If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
			}
		}
		MCHT ABC 1
		TNT1 A 0 A_TakeInventory("MacheteAmmo",1,TIF_NOTAKEINFINITE)
		TNT1 A 0 A_JumpIfInventory("PowerStrength2",1,3)
		MCHT A 0 {
			if(A_JumpIf(CountInv("PowerAmmoJunkie") > 0, "Null")) {
				A_SpawnItemEx("MacheterangProjectileJunkie",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			} Else {
				A_SpawnItemEx("MacheterangProjectile",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			}
		}
		TNT1 A 0 A_Jump(256,2)
		MCHT A 0 {
			if(A_JumpIf(CountInv("PowerAmmoJunkie") > 0, "Null")) {
				A_SpawnItemEx("MacheterangBerserkProjectileJunkie",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			} Else {
				A_SpawnItemEx("MacheterangBerserkProjectile",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			}
		}
		// This solution is ten times better. Just let it bouncecount out.
		MCHT D 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),1)
		MCHT E 1
		MCHT F 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing"),1)
		MCHT G 1
		MCHT H 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing"),1)
		MCHT I 1
		TNT1 A 3
		TNT1 A 0 A_JumpIf(CountInv("MacheteGoesFast") > 0 && CountInv("MacheteAmmo") > 0, "CaberMain")

		SPSN A 0 A_JumpIfInventory("ChromeUpgrade",1,2)
		PCFN A 0
		"####" ABC 1
		Goto ReadyMain
	Spawn:
		PCFG X -1
		Stop
	}
}

Actor ChromeJusticeSonicfire : FastProjectile
{
	Radius 10
	Height 12
	Speed 200
	Damage (2*random(1,3))
	Projectile
	MissileType "ChromeJusticeTrail"
	+RIPPER
	-DONTBLAST
	+BLOODLESSIMPACT
	+FORCERADIUSDMG
	+GETOWNER
	ProjectileKickBack 0
	ReactionTime 6
	DamageType Fire
	Decal "ChromeScorch"
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_PlaySound("ChromeJustice/Fly1",5,0.6,1)
		TNT1 A 0 A_PlaySound("ChromeJustice/Fly2",5,0.4,1)
		TNT1 A 1 A_Countdown
		Loop
	Death:
		TNT1 A 0 A_SpawnItemEx("ChromeImpact",0,0,0,0,0,0,0,SXF_CLIENTSIDE)
		TNT1 A 0 A_StopSound(5)
		TNT1 A 1 A_StopSound(6)
		Stop
	}
}

Actor ChromeImpact //Used for GLDEF
{
	+NOINTERACTION
	+NOGRAVITY
	States
	{
	Spawn:
		TNT1 A 65
		Stop
	}
}

Actor ChromeJusticeTrail
{
	+NOINTERACTION
	+NOGRAVITY
	+FORCERADIUSDMG
	Renderstyle Add
	Alpha 0.99
	Scale 0.05
	States
	{
	Spawn:
		NTCT A 0 NoDelay A_Explode((2*random(1,3)),24,0,0,24)
	Looplet:
		FRNG A 0 A_SpawnItemEx("Chrome_Fire",random(5,-5),random(5,-5),random(5,-5),frandom(0.3,0.2),frandom(0.3,0.2),frandom(0.3,0.2),frandom(0.0,360.0),SXF_CLIENTSIDE)
		FRNG A 0 A_SpawnItemEx("Chrome_Smoke",random(5,-5),random(5,-5),random(5,-5),frandom(0.1,0.2),frandom(0.1,0.2),frandom(0.1,0.2),frandom(0.0,360.0),SXF_CLIENTSIDE)
	Looplo:
		"####" "#" 0 A_SetScale(ScaleX+0.01,ScaleX)
		"####" "#" 1 Bright A_FadeOut(0.075)
		Loop
	}
}

Actor Chrome_Fire
{
	+NoInteraction
	+ForceXYBillboard
	Alpha 0.65
	RenderStyle Add
	States
	{
	Spawn:
		TNT1 AA 0 A_Jump(255, "SpriteA", "SpriteB", "SpriteC", "SpriteD",
							 "SpriteE", "SpriteF", "SpriteG", "SpriteH")
	SpriteA:
		SNCF A 0 A_Jump(255, "FadeIn")
		Goto SpriteA
	SpriteB:
		SNCF B 0 A_Jump(255, "FadeIn")
		Goto SpriteB
	SpriteC:
		SNCF C 0 A_Jump(255, "FadeIn")
		Goto SpriteC
	SpriteD:
		SNCF D 0 A_Jump(255, "FadeIn")
		Goto SpriteD
	SpriteE:
		SNCF E 0 A_Jump(255, "FadeIn")
		Goto SpriteE
	SpriteF:
		SNCF F 0 A_Jump(255, "FadeIn")
		Goto SpriteF
	SpriteG:
		SNCF G 0 A_Jump(255, "FadeIn")
		Goto SpriteG
	SpriteH:
		SNCF H 0 A_Jump(255, "FadeIn")
		Goto SpriteH
	FadeIn:
		"####" "#" 0 Bright A_SetScale(FRandom(0.25, 0.2))
		Goto Idle
	Idle:
		"####" "#" 2 Bright A_Jump(190, "FadeOut")
		Loop
	FadeOut:
		"####" "#" 1 Bright A_FadeOut(0.035)
		"####" "#" 0 A_ScaleVelocity(FRandom(1.01, 1.03))
		"####" "#" 0 A_SetScale(ScaleX - FRandom(0.001, 0.003))
		"####" "#" 0 A_JumpIf(ScaleX <= 0.0,"NULL")
		Loop

	}
}

Actor Chrome_Smoke
{
	+NoInteraction
	+ForceXYBillboard
	Alpha 0.0
	RenderStyle Translucent
	States
	{
	Spawn:
	    TNT1 A 3
		TNT1 A 0 A_Jump(255, "SpriteA", "SpriteB", "SpriteC", "SpriteD")
	SpriteA:
		DYMI A 0 A_Jump(255, "FadeIn")
		Goto SpriteA
	SpriteB:
		DYMI B 0 A_Jump(255, "FadeIn")
		Goto SpriteB
	SpriteC:
		DYMI C 0 A_Jump(255, "FadeIn")
		Goto SpriteC
	SpriteD:
		DYMI D 0 A_Jump(255, "FadeIn")
		Goto SpriteD
	FadeIn:
		"####" "#" 0 A_SetScale(FRandom(0.045, 0.055))
		Goto RealFadeIn
	RealFadeIn:
		"####" "#" 1 A_FadeIn(FRandom(0.01, 0.03))
		"####" "#" 0 A_JumpIf(Alpha >= 0.35, "Idle")
		Loop
	Idle:
		"####" "#" 5 A_Jump(200, "FadeOut")
		Loop
	FadeOut:
		"####" "#" 1 A_FadeOut(FRandom(0.01,0.02))
		"####" "#" 0 A_ScaleVelocity(0.96)
		"####" "#" 0 A_SetScale(ScaleX + FRandom(0.005, 0.002))
		Loop
	}
}

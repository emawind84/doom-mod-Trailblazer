Actor NutterSelected : Inventory { Inventory.MaxAmount 1 }
Actor NutterUpgrade : Inventory { Inventory.MaxAmount 1 }
Actor GloryShotTimer : Inventory { Inventory.MaxAmount 30 }

Actor Nutcracker : Weapon //replaces BFG9000
{
	Weapon.AmmoUse 1
	Weapon.AmmoType "TwoBoreAmmo"
	Weapon.AmmoUse2 3
	Weapon.AmmoType2 "TwoBoreAmmo"
	Weapon.AmmoGive 2
	Weapon.SlotNumber 7
	Weapon.SelectionOrder 3
	Weapon.UpSound "Nutcracker/Up"
	Weapon.Kickback 90
	Inventory.PickupSound "misc/w_pkup"
	Inventory.PickupMessage "Got the glorious Van Masterson's ''Nutcracker'' T-Rex rifle! Have you seen any T-Rexes around these days? Me neither! (7)"
	Obituary "%o landed on the wrong side of %k's ''Nutcracker'' barrel."
	Weapon.BobStyle InverseSmooth
	Weapon.BobSpeed 1.4
	Weapon.BobRangeY 0.5
	Weapon.BobRangeX 1.1
	+NOALERT
	+NOAUTOAIM
	States
	{
	Ready:
		NTFU A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCU A 0
		"####" ABCD 1
	ReadyMain:
		NTFG A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCG A 0
		"####" A 0 A_JumpIfInventory("MacheteGoesFast",1,"CaberToss")
		"####" A 1 A_WeaponReady(WRF_ALLOWZOOM)
		Loop
	Deselect:
		TNT1 A 0 A_TakeInventory("NutterSelected")
		TNT1 A 0 A_TakeInventory("PowerCrafterTimer")
		NTFU A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCU A 0
		"####" DCBA 1
		TNT1 A 0 A_StopSound(1)
		TNT1 A 0 SetPlayerProperty(0, 0, PROP_INSTANTWEAPONSWITCH)
		TNT1 AAAAAAAAAAAAAAA 0 A_Lower
		TNT1 A 1 A_Lower
		Wait
	Select:
		TNT1 A 0 A_GiveInventory("NutterSelected")
		TNT1 A 0 SetPlayerProperty(0, 1, PROP_INSTANTWEAPONSWITCH)
	HonkaCzongka:
		TNT1 A 0 A_Raise
		TNT1 A 1 A_Raise
		Loop
	Fire:
		NTCG A 0 {
			A_PlaySound("Nutcracker/Fire",1,0.7);
			A_FireCustomMissile("GunsmokeSpawner_Large_Dark",0,0,13,0,FPF_NOAUTOAIM);
			A_ChangeVelocity (Cos(Pitch) * -39, 0, Sin(Pitch) * 39, CVF_RELATIVE);
			A_Recoil(20.0);
			A_AlertMonsters;
			A_GunFlash;
		}
		NTCG A 0 A_JumpIfInventory("NutForceOne",1,"AviatorLoveMachine")
		Goto FireContinue
	AviatorLoveMachine:
		NTCG A 0 {
			A_PlaySound("Nutcracker/Airforce",0,0.6);
			A_FireCustomMissile("NutcrackerMinorBullet",-5.0,0,6,0,FPF_NOAUTOAIM);
			A_FireCustomMissile("NutcrackerMinorBullet",-10.0,0,6,0,FPF_NOAUTOAIM);
			A_FireCustomMissile("NutcrackerMinorBullet",5.0,0,6,0,FPF_NOAUTOAIM);
			A_FireCustomMissile("NutcrackerMinorBullet",10.0,0,6,0,FPF_NOAUTOAIM);
		}
	FireContinue:
		NTCG A 0 {
			A_FireCustomMissile("NutcrackerBullet",0,1,6,0,FPF_NOAUTOAIM);
			A_SetBlend("Yellow",0.2,12);
			If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 5);
				A_SetBlend("Blue",0.2,35);
			}
			A_QuakeEx(6,6,6,14,0,16,"none",QF_SCALEDOWN);
			A_SetAngle(Angle-2.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-2.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		NTFF A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCF A 0
		"####" A 1 Bright {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.9); }
			A_SetAngle(Angle-4.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-4.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" B 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.8); }
			A_SetAngle(Angle-3.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-3.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" B 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.85); }
			A_SetAngle(Angle-2.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-2.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" C 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.875); }
			A_SetAngle(Angle-2.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-2.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" D 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.9); }
			A_SetAngle(Angle-1.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-1.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" E 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.925); }
			A_SetAngle(Angle-1.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-1.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" F 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.95); }
			A_SetAngle(Angle-0.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-0.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" G 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.96); }
			A_SetAngle(Angle-0.25 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-0.25 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		TNT1 A 1 { if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.97); } }
		TNT1 A 1 { if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.98); } }
		TNT1 A 1 { if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.99); } }
		TNT1 A 1 { if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(1.0); } }
		TNT1 A 5 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),1)
		TNT1 A 5 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,1)
		TNT1 A 5 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,1)
		TNT1 A 0 A_JumpIfNoAmmo("Ready")
		
		NTFB A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCB A 0
		"####" A 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" BCD 1
		"####" E 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,2)
		"####" FGH 1
		"####" I 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,2)
		"####" JKL 1
		"####" M 0 A_PlaySound("Nutcracker/Load",4,0.8)
		"####" M 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" NOP 1
		"####" Q 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,2)
		"####" R 1
		
		NTFL A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCL A 0
		"####" AB 1
		"####" C 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,2)
		"####" DEF 1
		"####" G 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" HIK 1
		"####" A 0 A_PlaySound("Nutcracker/Pull",6,1.0)
		"####" L 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" MNO 1
		"####" P 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,4)
		
		NTFH A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCH A 0
		"####" A 1
		
		NTFH A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCH A 0
		"####" BC 1
		"####" D 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,2)
		"####" EF 1
		
		NTFG A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCG A 0
		"####" A 5
		Goto ReadyMain
	AltFire:
		NTCG A 0 A_JumpIfInventory("NutterUpgrade",1,1)
		Goto ReadyMain
		NTCG A 0 A_JumpIf(CountInv("PowerTimeFreezer") > 0 || CountInv("GloryShotTimer") > 0, "ReadyMain")
		NTCG A 0 {
			A_PlaySound("Nutcracker/Alt",CHAN_WEAPON|CHAN_NOPAUSE,1.0,0,ATTN_NONE);
			A_FireCustomMissile("GunsmokeSpawner_Large_Dark",0,0,13,0,FPF_NOAUTOAIM);
			A_ChangeVelocity (Cos(Pitch) * -39, 0, Sin(Pitch) * 39,CVF_RELATIVE);
			A_Recoil(20.0);
			A_AlertMonsters;
			A_GunFlash;
			If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 5|CHAN_NOPAUSE);
			}
		}
		NTCG A 0 A_JumpIfInventory("NutForceOne",1,"HereComesFreedomination")
		Goto AltFireContinue
	HereComesFreedomination:
		NTCG A 0 {
			A_FireCustomMissile("NutcrackerSlowBullet",-5.0,0,6,0,FPF_NOAUTOAIM);
			A_FireCustomMissile("NutcrackerSlowBullet",-10.0,0,6,0,FPF_NOAUTOAIM);
			A_FireCustomMissile("NutcrackerSlowBullet",5.0,0,6,0,FPF_NOAUTOAIM);
			A_FireCustomMissile("NutcrackerSlowBullet",10.0,0,6,0,FPF_NOAUTOAIM);
		}
	AltFireContinue:
		NTCG A 0 {
			A_FireCustomMissile("NutcrackerGloryShot",0,1,6,0,FPF_NOAUTOAIM);
			A_SetBlend("White",0.4,105);
			A_Quake(6,14,0,16,"none");
			A_SetAngle(Angle-2.5);
			A_SetPitch(Pitch-2.5);
		}
	
		NTFF A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCF A 0
		"####" A 1 Bright {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.9); }
			A_SetAngle(Angle-4.0, SPF_INTERPOLATE);
			A_SetPitch(Pitch-4.0, SPF_INTERPOLATE);
		}
		"####" B 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.8); }
			A_SetAngle(Angle-3.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-3.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" B 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.85); }
			A_SetAngle(Angle-2.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-2.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" C 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.875); }
			A_SetAngle(Angle-2.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-2.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" D 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.9); }
			A_SetAngle(Angle-1.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-1.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" E 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.925); }
			A_SetAngle(Angle-1.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-1.0 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" F 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.95); }
			A_SetAngle(Angle-0.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-0.5 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		"####" G 1 {
			if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.96); }
			A_SetAngle(Angle-0.25 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
			A_SetPitch(Pitch-0.25 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE);
		}
		TNT1 A 1 { if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.97); } }
		TNT1 A 1 { if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.98); } }
		TNT1 A 1 { if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(0.99); } }
		TNT1 A 1 { if(GetCvar("TB_ZoomFactor")) { A_ZoomFactor(1.0); } }
		TNT1 A 5 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),1)
		TNT1 A 5 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,1)
		TNT1 A 5 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,1)
		TNT1 A 0 A_JumpIfNoAmmo("Ready")
	
		NTFB A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCB A 0
		"####" A 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" BCD 1
		"####" E 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,2)
		"####" FGH 1
		"####" I 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,2)
		"####" JKL 1
		"####" M 0 A_PlaySound("Nutcracker/Load",4,0.8)
		"####" M 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" NOP 1
		"####" Q 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,2)
		"####" R 1
		
		NTFL A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCL A 0
		"####" AB 1
		"####" C 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,2)
		"####" DEF 1
		"####" G 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" HIK 1
		"####" L 0 A_PlaySound("Nutcracker/Pull",6,1.0)
		"####" L 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" MNO 1
		"####" P 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,4)
		
		NTFH A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCH A 0
		"####" A 1
		
		NTFH A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCH A 0
		"####" BC 1
		"####" D 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,2)
		"####" EF 1
		
		NTFG A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCG A 0
		"####" A 5
		Goto ReadyMain
	Flash:
		TNT1 A 3 A_Light2
		TNT1 A 6 A_Light2
		TNT1 A 0 A_Light0
		Stop	
	Zoom:
		"####" A 0 A_JumpIfInventory("ZoomReFire",1,"PunchHoes")
		NTFH A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCH A 0
		"####" FDBA 1
	PunchHoes:
		"####" A 0 {
				If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
			}
		}
		QML1 A 0 A_Overlay(2,"PunchFlash")
		QML1 A 0 A_JumpIfInventory("PowerStrength2",1,2)
		QML1 A 0 A_Jump(256,3)
		FML1 A 0 A_SetBlend("Orange",0.05,10)
		FML1 A 0 A_PlaySound("Melee/Firepunch",5)
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" A 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" B 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,2)
		"####" C 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" D 1 A_SetPitch(pitch-0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIfInventory("KineticFist",1,"KineticPunch")
		"####" A 0 A_JumpIfInventory("PowerStrength2",1,3)
		"####" A 0 A_CustomPunch(30, 1, 0, "PunchCracker", 100)
		"####" A 0 A_Jump(256,"PunchContinue")
		"####" A 0 A_CustomPunch(90, 1, 0, "FlamePunchCracker", 150,0.06)
		Goto PunchContinue
	KineticPunch:
		"####" A 0 A_JumpIfInventory("PowerStrength2",1,3)
		"####" A 0 A_CustomPunch(30, 1, 0, "PunchCracker", 300)
		"####" A 0 A_Jump(256,"PunchContinue")
		"####" A 0 A_CustomPunch(90, 1, 0, "FlamePunchCracker2", 350,0.06)
		Goto PunchContinue
	PunchContinue:
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,2)
		"####" E 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" F 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" G 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" H 1 A_SetPitch(pitch+0.3 * GetCvar("TB_WeaponRecoil"), SPF_INTERPOLATE)
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,2)
		"####" I 1
		"####" J 1
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,2)
		"####" K 1
		TNT1 A 5
		TNT1 A 0 A_JumpIf(GetPlayerInput(INPUT_BUTTONS) & (BT_ZOOM), "PunchHoes")
		
		NTFH A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCH A 0
		"####" ACEF 1
		Goto ReadyMain
	PunchFlash:
		NTFG D 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCG D 0
		"####" D 1 A_PlaySound("Punch/Swing",6,1.0)
		
		NTUM A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCM A 0
		"####" ABC 1
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" BA 1
		
		NTFG D 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCG D 0
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,2)
		"####" DD 1
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,2)
		"####" DD 1
		"####" A 0 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),2)
		"####" DD 1
		"####" A 0 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,2)
		"####" DD 1
		"####" A 0 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,2)
		"####" DD 1
		Stop
	CaberToss:
		"####" A 0 A_JumpIfInventory("MacheteAmmo",1,1)
		Goto ReadyMain
		NTFU A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCU A 0
		"####" DCBA 1
	CaberMain:
		TNT1 A 0 A_TakeInventory("MacheteAmmo",1,TIF_NOTAKEINFINITE)

		TNT1 A 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),1)
		TNT1 A 1
		TNT1 A 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,1)
		TNT1 A 1
		TNT1 A 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,1)
		TNT1 A 1
		TNT1 A 0 {
				A_PlaySound("Machete1/Slash",1,0.9);
				A_PlaySound("Machete2/Slash",5,0.2);
				If(CountInv("PowerQuadDamage")) {
				A_PlaySound("QuadDamage/Fire", 4);
			}
		}
		MCHT ABC 1
		TNT1 A 0 A_JumpIfInventory("PowerStrength2",1,3)
		MCHT A 0 {
			if(A_JumpIf(CountInv("PowerAmmoJunkie") > 0, "Null")) {
				A_SpawnItemEx("MacheterangProjectileJunkie",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			} Else {
				A_SpawnItemEx("MacheterangProjectile",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			}
		}
		TNT1 A 0 A_Jump(256,2)
		MCHT A 0 {
			if(A_JumpIf(CountInv("PowerAmmoJunkie") > 0, "Null")) {
				A_SpawnItemEx("MacheterangBerserkProjectileJunkie",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			} Else {
				A_SpawnItemEx("MacheterangBerserkProjectile",cos(-pitch)*48,0,32+(sin(-pitch)*48),cos(-pitch)*35,0,sin(-pitch)*35,0,SXF_NOCHECKPOSITION|SXF_TRANSFERPITCH);
			}
		}
		// This solution is ten times better. Just let it bouncecount out.
		MCHT D 1 A_JumpIf(Health <= 75 || CountInv("PowerBlazing") || CountInv("BlazingBooster"),1)
		MCHT E 1
		MCHT F 1 A_JumpIf(Health <= 50 || CountInv("PowerBlazing") > 0,1)
		MCHT G 1
		MCHT H 1 A_JumpIf(Health <= 25 || CountInv("PowerBlazing") > 0,1)
		MCHT I 1
		TNT1 A 3
		TNT1 A 0 A_JumpIf(CountInv("MacheteGoesFast") > 0 && CountInv("MacheteAmmo") > 0, "CaberMain")

		NTFU A 0 A_JumpIfInventory("NutterUpgrade",1,2)
		NTCU A 0
		"####" ABCD 1
		Goto ReadyMain
	Spawn:
		NTCG X -1
		Stop
	}
}

Actor NutcrackerBullet : FastProjectile
{
  Radius 19
  Height 14
  MissileHeight 8
  Speed 200
  Damage (20*random(1,5))
  Projectile
  +NOGRAVITY
  +RIPPER
  +EXTREMEDEATH
  +GETOWNER
  Renderstyle Add
  Obituary "%o had the very unfortunate situation to land in the %k's ''Nutcracker'' line of fire. Nothing was left after."
  Scale 1
  MissileType "NutcrackerTrail"
  Decal "NutcrackerChip"
  var int User_Counter;
  States
  {
  Spawn:
    TRAC A 0 NoDelay A_PlaySound("Nutcracker/Fly",5,0.8,1)
	TRAC A 1 Bright A_ChangeVelocity(0, 0, -0.15, CVF_RELATIVE)
	Loop
  Death:
	TNT1 A 0 A_SetUserVar("User_Counter",40)
  Looplet:
    TNT1 A 0 A_JumpIf(User_Counter <= 0, "Rest")
    TNT1 A 0 A_SetUserVar("User_Counter", User_Counter - 1)
	TNT1 A 0 A_SpawnItemEx("NutcrackerShatterBullet",0,0,0,frandom(125.0,100.0),0,-frandom(12.0,-12.0),frandom(0.0,360.0),SXF_TRANSFERPOINTERS,0)
	Loop
  Rest:
    TNT1 A 0 A_PlaySound("Nutcracker/Hit",5,0.8)
	TNT1 A 0 A_SpawnItemEx("NutcrackerPuff",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
	TNT1 A 25 A_QuakeEx(4,4,4,25,0,384,"none",QF_SCALEDOWN)
    Stop
  /*XDeath:
	TNT1 A 1
	Stop*/
  }
}

Actor NutcrackerShatterBullet : FastProjectile
{
  Radius 11
  Height 7
  MissileHeight 8
  Speed 125
  Damage (5*random(1,5))
  Projectile
  +NOGRAVITY
  +BLOODLESSIMPACT
  +FORCERADIUSDMG
  +EXTREMEDEATH
  Renderstyle Add
  Obituary "%o was caught by %k's stray ''Nutcracker'' impact pellet."
  Scale 0.5
  MissileType "ThrottleTrail"
  Decal "BulletChip2"
  States
  {
  Spawn:
	TRAC A 1 Bright A_ChangeVelocity(0, 0, -0.4, CVF_RELATIVE)
	Loop
  Death:
	TNT1 A 1 A_SpawnItemEx("BuellerPuff")
  XDeath:
	TNT1 A 0 A_Explode(90,96,0)
	Stop
  }
}

Actor NutcrackerMinorBullet : NutcrackerBullet
{
  Radius 11
  Height 8
  MissileHeight 8
  Speed 200
  Damage (5*random(1,5))
  Renderstyle Add
  Obituary "%o faced the Nut Force One."
  MissileType "NutcrackerMinorTrail"
  Decal "NutcrackerMinorChip"
  States
  {
  Spawn:
    TRAC A 0 NoDelay A_PlaySound("Nutcracker/Fly",5,0.8,1)
	TRAC A 1 Bright A_ChangeVelocity(0, 0, -0.15, CVF_RELATIVE)
	Loop
  Death:
	TNT1 A 0 A_SetUserVar("User_Counter",10)
  Looplet:
    TNT1 A 0 A_JumpIf(User_Counter <= 0, "Rest")
    TNT1 A 0 A_SetUserVar("User_Counter", User_Counter - 1)
	TNT1 A 0 A_SpawnItemEx("NutcrackerShatterBullet",0,0,0,frandom(125.0,100.0),0,-frandom(12.0,-12.0),frandom(0.0,360.0),SXF_TRANSFERPOINTERS,0)
	Loop
  Rest:
    TNT1 A 0 A_PlaySound("Nutcracker/Hit",5,0.5)
	TNT1 A 0 A_SpawnItemEx("NutcrackerPuff",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
	TNT1 A 25 A_QuakeEx(3,3,3,25,0,256,"none",QF_SCALEDOWN)
    Stop
  }
}

Actor NutcrackerTrail
{
	+NOINTERACTION
	+FORCERADIUSDMG
	+EXTREMEDEATH
	Renderstyle Add
	Alpha 0.5
	Scale 0.05
	States
	{
	Spawn:
	NTCT A 0 NoDelay A_Explode((50*random(1,5)),56,0)
	Looplet:
	NTCT A 1 Bright A_FadeOut(frandom(0.15,0.2))
	NTCT A 0 A_SetScale(ScaleX+0.05)
	Loop
	}
}

Actor NutcrackerMinorTrail : NutcrackerTrail
{
	Scale 0.04
	States
	{
	Spawn:
	NTCT A 0 NoDelay A_Explode((10*random(1,5)),32,0)
	Looplet:
	NTCT A 1 Bright A_FadeOut(frandom(0.15,0.2))
	NTCT A 0 A_SetScale(ScaleX+0.02)
	Loop
	}
}

//The glory of Alt-Fire
ACTOR NutterReturn : CustomInventory
{
  Inventory.MaxAmount 0
  +AUTOACTIVATE +ALWAYSPICKUP
  States
  {
  Spawn:
      TNT1 A -1
	  Stop
  Pickup:
      TNT1 A 0 A_SetBlend("Red",0.2,50)
	  Stop
  }
}

Actor NutcrackerGloryShot : NutcrackerBullet
{
  Speed 5
  +NOTIMEFREEZE
  +SKYEXPLODE
  MissileType ""
  var int User_Blastspan;
  Scale 1.2
  Renderstyle Normal
  States
  {
  Spawn:
    FREB A 0 NoDelay ACS_NamedExecuteAlways("GloryStart")	//A_PlaySound("Nutcracker/Fly",5,0.8,1)
	FREB A 0 A_ChangeVelocity(0, 0, 0.1, CVF_RELATIVE)
	FREB A 0 A_PlaySound("Nutcracker/Chariots",CHAN_5|CHAN_NOPAUSE,1.0,1,ATTN_NONE)
	FREB A 0 A_GiveToTarget("NutcrackerTimeStop")
  Looplet:
	FREB A 0 A_RadiusGive("NutcrackerGloryItem",1024,RGF_MONSTERS,1)
	FREB A 0 A_SpawnItemEx("NutcrackerGloryTrailA",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLIENTSIDE)
	FREB A 0 A_ChangeVelocity(0, 0, -0.005, CVF_RELATIVE)
	FREB AAAAAAAAAAAA 1 Bright A_SpawnItemEx("NutcrackerGloryParticler",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLIENTSIDE)
	
	FREB A 0 A_RadiusGive("NutcrackerGloryItem",1024,RGF_MONSTERS,1)
	FREB A 0 A_SpawnItemEx("NutcrackerGloryTrailB",0,0,0,0,0,0,0,SXF_ORIGINATOR|SXF_TRANSFERPOINTERS|SXF_CLIENTSIDE)
	FREB A 0 A_ChangeVelocity(0, 0, -0.005, CVF_RELATIVE)
	FREB AAAAAAAAAAAA 1 Bright A_SpawnItemEx("NutcrackerGloryParticler",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLIENTSIDE)
	
	FREB A 0 A_RadiusGive("NutcrackerGloryItem",1024,RGF_MONSTERS,1)
	FREB A 0 A_SpawnItemEx("NutcrackerGloryTrailC",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLIENTSIDE)
	FREB A 0 A_ChangeVelocity(0, 0, -0.005, CVF_RELATIVE)
	FREB AAAAAAAAAAAA 1 Bright A_SpawnItemEx("NutcrackerGloryParticler",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLIENTSIDE)
	Loop
  Death:
	TNT1 A 0 A_SetUserVar("User_Counter",80)
	TNT1 A 0 A_GiveToTarget("GloryShotTimer",30)
	TNT1 A 0 A_GiveToTarget("GloryTimer")
	TNT1 A 0 A_StopSound(5)
  Looplet:
    TNT1 A 0 A_JumpIf(User_Counter <= 0, "Rest")
    TNT1 A 0 A_SetUserVar("User_Counter", User_Counter - 1)
	TNT1 A 0 A_SpawnItemEx("NutcrackerShatterBullet",0,0,0,frandom(125.0,100.0),0,frandom(16.0,-16.0),frandom(0.0,360.0),SXF_TRANSFERPOINTERS,0)
	Loop
  Rest:
	TNT1 A 0 A_TakeFromTarget("PowerTimeFreezer",1)
	TNT1 A 0 ACS_NamedExecuteAlways("GloryEnd")
	TNT1 A 0 A_SetUserVar("User_Blastspan",14)
	TNT1 A 0 A_PlaySound("Spikebomb/Explosion",5|CHAN_NOPAUSE,0.9)
	TNT1 A 0 A_PlaySound("Spikebomb/Add",7|CHAN_NOPAUSE)
	TNT1 A 0 A_PlaySound("Spikebomb/Far",6|CHAN_NOPAUSE,0.8)
	TNT1 A 0 A_SpawnItemEx("FreedomMain",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
	TNT1 A 0 A_Quake(3,55,0,768,"none")
	TNT1 A 0 A_Quake(7,55,0,384,"none")
	TNT1 A 0 A_Explode(3000,364,0,1)
  DeathLoop:
	TNT1 A 0 A_SetUserVar("User_Blastspan", User_Blastspan - 1)
	TNT1 A 4 A_Explode(40,364,0,1)
	TNT1 A 0 A_JumpIf(User_Blastspan <= 0, 1)
	Loop
	TNT1 A 6
	Stop
  /*XDeath:
	TNT1 A 1
	Stop*/
  }
}

Actor NutcrackerSlowBullet : NutcrackerBullet
{
  Speed 5
  +NOTIMEFREEZE
  MissileType ""
  var int User_Cycle;
  States
  {
  Spawn:
	TRAC A 0 NoDelay A_ChangeVelocity(0, 0, 0.1, CVF_RELATIVE)
  Reset:
	TRAC A 0 A_SpawnItemEx("NutcrackerGloryTrailA",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS|SXF_CLIENTSIDE)
	TRAC A 0 A_ChangeVelocity(0, 0, -0.005, CVF_RELATIVE)
	TRAC A 0 A_SetUserVar("User_Cycle",0)
  SpawnMain:
	TRAC A 1 Bright A_SetUserVar("User_Cycle", User_Cycle + 1)
	TRAC A 0 A_JumpIf(User_Cycle >= 12, "Reset")
	TRAC A 0 A_JumpIfInTargetInventory("PowerTimeFreezer", 1, "SpawnMain") //This is one of those moments when I wanna cry that there's no better way to handle this, like A_JumpIfInTarget or something...
	Goto Spawn2
  Spawn2:
	TRAC A 0 A_ScaleVelocity(40.0)
  SpawnMain2:
	TRAC A 0 A_SpawnItemEx("NutcrackerTrail",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
  	TRAC A 1 Bright A_ChangeVelocity(0, 0, -0.15, CVF_RELATIVE)
	Loop
  Death:
	TNT1 A 0 A_SetUserVar("User_Counter",40)
  Looplet:
    TNT1 A 0 A_JumpIf(User_Counter <= 0, "Rest")
    TNT1 A 0 A_SetUserVar("User_Counter", User_Counter - 1)
	TNT1 A 0 A_SpawnItemEx("NutcrackerShatterBullet",0,0,0,frandom(125.0,100.0),0,-frandom(12.0,-12.0),frandom(0.0,360.0),SXF_TRANSFERPOINTERS,0)
	Loop
  Rest:
    TNT1 A 0 A_PlaySound("Nutcracker/Hit",5,0.8)
	TNT1 A 0 A_SpawnItemEx("NutcrackerPuff",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
	TNT1 A 25 A_QuakeEx(4,4,4,25,0,384,"none",QF_SCALEDOWN)
    Stop
  /*XDeath:
	TNT1 A 1
	Stop*/
  }
}

//Trail and flight eyecandy
Actor NutcrackerGloryTrailA : NutcrackerTrail
{
	+NOTIMEFREEZE
	Scale 0.2
	Alpha 0.0
	States
	{
	Spawn:
		NTCT A 0
		Goto Blast
	Blast:
		"####" "#" 0 A_Explode((50*random(1,5)),56,0)
		Goto PhaseOne
	PhaseOne:
		"####" "#" 0 A_JumpIfInTargetInventory("PowerTimeFreezer",1,1)
		Goto Dissipate
		"####" "#" 1 Bright A_JumpIf(Alpha >= 0.5,"Looplet")
		"####" "#" 0 A_FadeIn(0.05)
		"####" "#" 0 A_SetScale(ScaleX+0.01)
		Loop
	Looplet:
		"####" "#" 0 A_JumpIfInTargetInventory("PowerTimeFreezer",1,1)
		Goto Dissipate
		"####" "#" 1 Bright A_JumpIf(ScaleX>1.5,"PhaseTwo")
		"####" "#" 0 A_SetScale(ScaleX+0.01)
		Loop
	PhaseTwo:
		"####" "#" 0 A_JumpIfInTargetInventory("PowerTimeFreezer",1,1)
		Goto Dissipate
		"####" "#" 1 Bright A_FadeOut(0.005)
		"####" "#" 0 A_SetScale(ScaleX+0.01)
		Loop
	Dissipate:
		"####" "#" 1 Bright A_FadeOut(0.05)
		"####" "#" 0 A_SetScale(ScaleX+0.05)
		Loop
	}
}

Actor NutcrackerGloryTrailB : NutcrackerGloryTrailA
{
	States
	{
	Spawn:
		NTCT B 0
		Goto Blast
	}
}

Actor NutcrackerGloryTrailC : NutcrackerGloryTrailA
{
	States
	{
	Spawn:
		NTCT C 0
		Goto Blast
	}
}

Actor NutcrackerGloryParticler
{
	+NOTIMEFREEZE
	+NOINTERACTION
	Var Int User_Counter;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetUserVar("User_Counter",random(8,10))
	Main:
		"####" "#" 0 A_JumpIfInTargetInventory("PowerTimeFreezer",1,1)
		Goto Null
		TNT1 A 0 A_SpawnItemEx("NutcrackerGloryParticle",frandom(512.0,-512.0),
		frandom(512.0,-512.0),frandom(512.0,-512.0),frandom(0.1,1.5),
		frandom(0.1,1.5),frandom(frandom(0.1,1.5),frandom(-0.1,-1.5)),
		frandom(0.0,360.0),SXF_CLIENTSIDE|SXF_TRANSFERPOINTERS)
		TNT1 A 0 A_SetUserVar("User_Counter", User_Counter - 1)
		TNT1 A 0 A_JumpIf(User_Counter <= 0, "Null")
		TNT1 A random(1,3) A_SetAngle(Angle + 15) //To make it less box-feeling - rotate the box to make from it a circle. Simple as that.
		Loop
	}
}

Actor NutcrackerGloryParticle
{
	+NOTIMEFREEZE
	+NOINTERACTION
	Renderstyle Add
	Alpha 0.0
	Scale 0.0
	States
	{
	Spawn:
		TNT1 A 0 NoDelay A_SetScale(frandom(0.03,0.02))
	SpriteSetup:
		TNT1 A 0 A_Jump(256,"SpriteA","SpriteB","SpriteC")
	SpriteA:
		FREF A 0
		Goto FadeIn
	SpriteB:
		FREF B 0
		Goto FadeIn
	SpriteC:
		FREF C 0
		Goto FadeIn
	FadeIn:
		"####" "#" 1 Bright A_FadeIn(frandom(0.025,0.075))
		"####" "#" 0 A_SetScale(ScaleX * 1.02)
		"####" "#" 0 A_JumpIf(Alpha >= 0.95, "PhaseTwo")
		Loop
	PhaseTwo:
		"####" "#" 0 A_JumpIfInTargetInventory("PowerTimeFreezer",1,1)
		Goto Dissipate
		"####" "#" 0 A_ChangeVelocity(frandom(-1.0,1.0),frandom(1.0,-1.0),frandom(1.0,-1.0),CVF_RELATIVE)
		"####" "#" 0 A_SetScale(ScaleX * 0.99)
		"####" "###" 1 Bright A_FadeOut(frandom(0.05,0.025))
		"####" "###" 1 Bright A_FadeIn(frandom(0.035,0.025))
		Loop
	Dissipate:
		"####" "#" 0 A_SetScale(ScaleX * 0.99)
		"####" "#" 1 Bright A_FadeOut(0.1)
		Loop
	}
}

//Glory Shot passby effect
ACTOR NutcrackerGloryItem : CustomInventory
{
   Inventory.Amount 0
   Inventory.PickupMessage ""
   Inventory.PickupSound ""
   Inventory.PickupFlash None 
   +INVENTORY.ALWAYSPICKUP
   +INVENTORY.AUTOACTIVATE
   +INVENTORY.NOSCREENFLASH
   States
   {
   Spawn:
	TNT1 A -1
	Stop
   Pickup:
	TNT1 A 0 A_JumpIfInventory("PowerGloryShotDelay",1,"Null")
	TNT1 A 0 A_GiveInventory("GloryShotPrimer")
	TNT1 A 0 A_GiveInventory("GloryShotInnardExploder")
	Stop
   }
}

ACTOR GloryShotPrimer : PowerupGiver //To prevent blast overload, there must be such locker.
{
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	+INVENTORY.ADDITIVETIME
	powerup.duration -10
	Powerup.Type GloryShotDelay
	Powerup.Color None 0.0
	Inventory.PickupMessage "Dun dunn dunnn dun duuun. Dun dunnn dun dun duuuun."
	States
	{
	Spawn:
		TNT1 A -1
		Stop
	}
}

actor PowerGloryShotDelay : PowerProtection
{
	damagefactor "Normal", 1.0
}

ACTOR GloryShotInnardExploder : NutcrackerGloryItem
{
   States
   {
   Pickup:
	TNT1 A 0 A_SpawnItemEx("GloryShotBlast",0,0,0,0,0,0,0,SXF_TRANSFERPOINTERS)
	Stop
   }
}

ACTOR GloryShotBlast
{
	+NOINTERACTION
	+FORCERADIUSDMG
	Damagetype Patriarch
	States
	{
	Spawn:
		TNT1 B random(2,10) //So you won't see GLDEF going off during timefreeze. :V
		TNT1 A 0 A_PlaySound("Explosion/Init",5,0.8)
		TNT1 A 0 A_PlaySound("Explosion/Main",6,0.8)
		TNT1 A 0 A_PlaySound("Explosion/Layer",4,0.8)
		TNT1 A 0 A_PlaySound("Explosion/Flavor",7,0.8)
		TNT1 A 0 A_SpawnItemEx("Patriarch_Main",0,0,0,0,0,0,SXF_CLIENTSIDE,0)
		TNT1 A 0 A_Explode(175,148,0)
		TNT1 A 28 A_Quake(3,12,0,512,"none")
		Stop
	}
}

//Glory shot timer item
ACTOR GloryTimer : NutcrackerGloryItem
{
   States
   {
   Spawn:
	TNT1 A -1
	Stop
   Pickup:
	TNT1 A 0 ACS_NamedExecuteAlways("Glory Blockade")
	Stop
   }
}

//Glory Shot impact blast
Actor FreedomBase
{ 
    +NOINTERACTION
	+NOTONAUTOMAP
	+FORCEXYBILLBOARD
	Alpha 0.0
	Scale 0.0
}

Actor FreedomFX : FreedomBase
{ 
	States
	{
		Spawn:
			TNT1 A 80
			Stop
	}
}

Actor FreedomSmoke : FreedomBase
{
	RenderStyle Translucent
	States
	{
		Spawn:
			TNT1 A random(12,17)
			TNT1 A 0 //NoDelay A_ScaleVelocity(frandom(0.8,1.1))
		SetupSprite:
			TNT1 A 0 A_Jump(256, "SpriteA", "SpriteB", "SpriteC", "SpriteD",
								 "SpriteE", "SpriteF", "SpriteG", "SpriteH",
								 "SpriteI", "SpriteJ", "SpriteK", "SpriteL")
		SpriteA:
			FRES A 0
			Goto SetupScale
		SpriteB:
			FRES B 0
			Goto SetupScale
		SpriteC:
			FRES C 0
			Goto SetupScale
		SpriteD:
			FRES D 0
			Goto SetupScale
		SpriteE:
			FRES E 0
			Goto SetupScale
		SpriteF:
			FRES F 0
			Goto SetupScale
		SpriteG:
			FRES G 0
			Goto SetupScale
		SpriteH:
			FRES H 0
			Goto SetupScale
		SpriteI:
			FRES I 0
			Goto SetupScale
		SpriteJ:
			FRES J 0
			Goto SetupScale
		SpriteK:
			FRES K 0
			Goto SetupScale
		SpriteL:
			FRES L 0
			Goto SetupScale
		SetupScale:
			"####" "#" 0 A_SetScale(frandom(0.9, 1.1))
		FadeIn:
			"####" "#" 1 A_FadeIn(FRandom(0.01, 0.04))
			"####" "#" 0 A_ChangeVelocity(frandom(-0.02, 0.02), frandom(-0.02, 0.02), frandom(-0.02, 0.03), 0)
			"####" "#" 0 A_SetScale(ScaleX + FRandom(0.01, 0.02))
			"####" "#" 0 A_JumpIf(Alpha >= 0.4, "Inter")
			Loop
		Inter:
			"####" "#" 0 A_ChangeVelocity(frandom(-0.02, 0.02), frandom(-0.02, 0.02), frandom(-0.02, 0.03), 0)
			"####" "#" 0 A_SetScale(ScaleX + FRandom(0.01, 0.02))
			"####" "#" 1 A_Jump(24,"FadeOut")
			Loop
		FadeOut:
			"####" "#" 1 A_FadeOut(FRandom(0.01,0.02))
			"####" "#" 0 A_ChangeVelocity(frandom(-0.02, 0.02), frandom(-0.02, 0.02), frandom(-0.02, 0.03), 0)
			"####" "#" 0 A_SetScale(ScaleX + FRandom(0.01, 0.02))
			Loop
	}
}

Actor FreedomCloud : FreedomBase
{
	RenderStyle Add
	States
	{
		Spawn:
			TNT1 A 0
		SetupSprite:
			TNT1 A 0 A_Jump(256, "SpriteA", "SpriteB", "SpriteC", "SpriteD",
			"SpriteE", "SpriteF", "SpriteG", "SpriteH")
		SpriteA:
			CLOD A 0
			Goto SetupScale
		SpriteB:
			CLOD B 0
			Goto SetupScale
		SpriteC:
			CLOD C 0
			Goto SetupScale
		SpriteD:
			CLOD D 0
			Goto SetupScale
		SpriteE:
			CLOD E 0
			Goto SetupScale
		SpriteF:
			CLOD F 0
			Goto SetupScale
		SpriteG:
			CLOD G 0
			Goto SetupScale
		SpriteH:
			CLOD H 0
			Goto SetupScale
		SetupScale:
			"####" "#" 0 A_SetScale(frandom(1.2, 1.65))
		FadeIn:
			"####" "#" 1 A_FadeIn(FRandom(0.15, 0.1))
			"####" "#" 0 A_ChangeVelocity(frandom(-0.06, 0.06), frandom(-0.06, 0.06), frandom(-0.06, 0.07), 0)
			"####" "#" 0 A_SetScale(ScaleX + FRandom(0.01, 0.025))
			"####" "#" 0 A_JumpIf(Alpha >= 0.8, "FadeOut")
			Loop
		FadeOut:
			"####" "#" 1 A_FadeOut(FRandom(0.05,0.02))
			"####" "#" 0 A_ChangeVelocity(frandom(-0.06, 0.06), frandom(-0.06, 0.06), frandom(-0.06, 0.07), 0)
			"####" "#" 0 A_SetScale(ScaleX + FRandom(0.025, 0.05))
			Loop
	}
}

Actor FreedomBlast : FreedomBase
{
	RenderStyle Add
	States
	{
		Spawn:
			TNT1 A 0
		SetupSprite:
			TNT1 A 0 A_Jump(255, "SpriteA", "SpriteB", "SpriteC", "SpriteD",
			"SpriteE", "SpriteF")
		SpriteA:
			FBST A 0
			Goto SetupScale
		SpriteB:
			FBST B 0
			Goto SetupScale
		SpriteC:
			FBST C 0
			Goto SetupScale
		SpriteD:
			FBST D 0
			Goto SetupScale
		SpriteE:
			FBST E 0
			Goto SetupScale
		SpriteF:
			FBST F 0
			Goto SetupScale
		SetupScale:
			"####" "#" 0 A_SetScale(frandom(1.2, 1.65))
		FadeIn:
			"####" "#" 1 A_FadeIn(FRandom(0.15, 0.1))
			"####" "#" 0 A_ChangeVelocity(frandom(-0.06, 0.06), frandom(-0.06, 0.06), frandom(-0.06, 0.07), 0)
			"####" "#" 0 A_SetScale(ScaleX + FRandom(0.02, 0.01))
			"####" "#" 0 A_JumpIf(Alpha >= 0.8, "FadeOut")
			Loop
		FadeOut:
			"####" "#" 1 A_FadeOut(FRandom(0.05,0.02))
			"####" "#" 0 A_ChangeVelocity(frandom(-0.06, 0.06), frandom(-0.06, 0.06), frandom(-0.06, 0.07), 0)
			"####" "#" 0 A_SetScale(ScaleX + FRandom(0.02, 0.01))
			Loop
	}
}

Actor FreedomEmber : FreedomBase
{
	RenderStyle Add
	States
	{
		Spawn:
			TNT1 A 0
		SetupSprite:
			TNT1 A 0 A_Jump(256, "SpriteA", "SpriteB", "SpriteC", "SpriteD",
			"SpriteE", "SpriteF")
		SpriteA:
			FREM A 0
			Goto SetupScale
		SpriteB:
			FREM B 0
			Goto SetupScale
		SpriteC:
			FREM C 0
			Goto SetupScale
		SpriteD:
			FREM D 0
			Goto SetupScale
		SpriteE:
			FREM E 0
			Goto SetupScale
		SpriteF:
			FREM F 0
			Goto SetupScale
		SetupScale:
			"####" "#" 0 A_SetScale(frandom(0.04, 0.1))
		FadeIn:
			"####" "#" 1 A_FadeIn(FRandom(0.99, 0.9))
		Intern:
			"####" "#" 3 Bright A_Jump(200, "FadeOut")
			Loop
		FadeOut:
			"####" "#" 1 A_Weave(FRandom(0.9, 1.0), FRandom(0.9, 1.0),
										FRandom(70, 90), FRandom(70, 90))
			"####" "#" 0 A_JumpIf(ScaleX <= 0.0, "NULL")
			"####" "#" 0 A_ChangeVelocity(VelX * FRandom(0.93, 0.99),
										  VelY * FRandom(0.93, 0.99),
										  VelZ - FRandom(0.1, 0.2),
										  CVF_REPLACE)
			"####" "#" 0 A_SetScale(ScaleX - FRandom(0.002, 0.001))
			Loop
	}
}

Actor FreedomShockwaveA : FreedomBase
{
	RenderStyle Add
	Alpha 0.99
	Scale 0.9
	States
	{
		Spawn:
			NTCT A 0
			Goto Looplet
		Looplet:
			"####" "#" 0 A_SetScale(ScaleX + FRandom(0.12, 0.09))
			"####" "#" 1 Bright A_FadeOut(frandom(0.02,0.05))
			Loop
	}
}

Actor FreedomShockwaveB : FreedomShockwaveA
{
	Alpha 0.99
	Scale 1.1
	States
	{
		Spawn:
			NTCT B 0
			Goto Looplet
		Looplet:
			"####" "#" 0 A_SetScale(ScaleX + FRandom(0.12, 0.09))
			"####" "#" 1 Bright A_FadeOut(frandom(0.02,0.05))
			Loop
	}
}

Actor FreedomShockwaveC : FreedomShockwaveA
{
	Alpha 0.99
	Scale 1.3
	States
	{
		Spawn:
			NTCT C 0
			Goto Looplet
		Looplet:
			"####" "#" 0 A_SetScale(ScaleX + FRandom(0.12, 0.09))
			"####" "#" 1 Bright A_FadeOut(frandom(0.02,0.05))
			Loop
	}
}

Actor FreedomFlare : FreedomBase
{
	RenderStyle Add
	Alpha 0.99
	Scale 7.5
	States
	{
		Spawn:
			TNT1 A 0
		SetupSprite:
			TNT1 A 0 A_Jump(256, "SpriteA", "SpriteB", "SpriteC")
		SpriteA:
			FREF A 0
			Goto Looplet
		SpriteB:
			FREF B 0
			Goto Looplet
		SpriteC:
			FREF C 0
			Goto Looplet
		Looplet:
			"####" "#" 1 Bright A_FadeOut(0.02)
			"####" "#" 0 A_SetScale(ScaleX - FRandom(0.05, 0.15))
			"####" "#" 0 A_JumpIf(ScaleX <= 0.0, "NULL")
			Loop
	}
}

Actor FreedomExplosionMain : FreedomBase
{
	Var Int User_Counter;
	States
	{
		Spawn:
			TNT1 A 0 NoDelay A_SetUserVar("User_Counter", 30)
			Goto Emit
		Emit:
			TNT1 A 0 A_SpawnItemEx("FreedomCloud", FRandom(-45.0, 45.0),
									FRandom(-45.0, 45.0), FRandom(-45.0, 45.0),
									FRandom(1.0, 3.5), 0,
									FRandom(-1.0, 4.5), FRandom(0.0,360.0), SXF_CLIENTSIDE, 60)
			TNT1 A 0 A_SpawnItemEx("FreedomBlast", FRandom(-45.0, 45.0),
									FRandom(-45.0, 45.0), FRandom(-45.0, 45.0),
									FRandom(1.0, 3.5), 0,
									FRandom(-2.0, 2.5), FRandom(0.0,360.0), SXF_CLIENTSIDE, 60)			
		    TNT1 A 0 A_JumpIf(GetCvar("TB_SmokeReduction")==2, 2)	
			TNT1 A 0 A_SpawnItemEx("FreedomSmoke", FRandom(-45.0, 45.0),
									FRandom(-45.0, 45.0), FRandom(-45.0, 45.0),
									FRandom(0.5, 2.5), 0,
									FRandom(-2.0, 2.5), FRandom(0.0,360.0), SXF_CLIENTSIDE, 80)
			TNT1 AA 0 A_SpawnItemEx("FreedomEmber", FRandom(-35.0, 35.0),
									FRandom(-35.0, 35.0), FRandom(-45.0, 45.0),
									FRandom(5.0, 17.0), 0,
									FRandom(2.0, 9.0), FRandom(0.0,360.0), SXF_CLIENTSIDE, 40)
			TNT1 A 1
			TNT1 A 0 A_SetUserVar("User_Counter", User_Counter - 1)
			TNT1 A 0 A_JumpIf(User_Counter <= 0,"NULL")
			Loop
	}
}

Actor FreedomMain : FreedomBase
{
	States
	{
	Spawn:
	TNT1 A 0 NoDelay A_SpawnItemEx("FreedomExplosionMain",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
	TNT1 A 0 A_SpawnItemEx("FreedomFlare",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
	TNT1 A 0 A_SpawnItemEx("FreedomShockwaveA",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
	TNT1 A 0 A_SpawnItemEx("FreedomShockwaveB",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
	TNT1 A 0 A_SpawnItemEx("FreedomShockwaveC",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
	TNT1 A 0 A_SpawnItemEx("FreedomFX",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0)
	Stop
	}
}


actor NutcrackerTimeStop : PowerupGiver
{
	Inventory.Amount 0
	powerup.type "TimeFreezer"
	powerup.duration -0x7FFFFFFF
	//inventory.icon "NUTJOB0"
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	states
	{
	Spawn:
		TNT1 A 1
		stop
	}
}
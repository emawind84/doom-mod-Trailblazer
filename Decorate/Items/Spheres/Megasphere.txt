ACTOR MegasphereItem : Megasphere
{
	Inventory.PickupMessage "$GOTMEGASPHERE"
	Inventory.PickupSound "Megasphere/Pickup"
	Renderstyle Add
	+FLOATBOB
	Var Int User_Count;
	States
	{
	Spawn:
		TBMS AAAABBBBCCCCDDDDEEEEFFFFEEEEDDDDCCCCBBBB 1 Bright {
			If(!(A_CheckSight("Null") || GetCVar("TB_ParticleReduction")))
			{
				If(A_JumpIf(User_Count % 4 == 0, "Null"))
				{
					A_SpawnItemEx("MegaFade",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0);
					A_SetUserVar(User_Count, 0);
				}
				A_SetUserVar(User_Count, User_Count + 1);
				A_SpawnItemEx("MegasphereParticle",
				frandom(10.0,-10.0),frandom(10.0,-10.0),35+frandom(10.0,-10.0),
				frandom(0.6,-0.6),frandom(0.6,-0.6),frandom(1.2,0.4),
				frandom(0.0,360.0),SXF_CLIENTSIDE,64);
			}
		}
		Loop
	Pickup:
		TNT1 A 0 {
			//Alchemist's Dream storing.
			if(CountInv("AlchemistsDream") && (CountInv("MegaspherePortable") < 3) && GetPlayerInput(INPUT_BUTTONS) & BT_USE)
			{
				A_Print("$MEGASPHERESTORED");
				A_GiveInventory("MegaspherePortable",1);
				Return State("Null");
			}
			//Nicotine rush warnings.
			If(CountInv("NicotineToken") >= 75 && !CountInv("NicotineAchtung1")) { A_Print("\c[white]about 25 cigs to nicotine rush."); A_GiveInventory("NicotineAchtung1",1); }
			If(CountInv("NicotineToken") >= 90 && !CountInv("NicotineAchtung2")) { A_Print("\c[white]about 10 cigs to nicotine rush."); A_GiveInventory("NicotineAchtung2",1); }
			If(CountInv("NicotineToken") >= 95 && !CountInv("NicotineAchtung3")) { A_Print("\c[gold]about 5 cigs to nicotine rush!");   A_GiveInventory("NicotineAchtung3",1); }
			
			//Blazing Booster bonus.
			If(CountInv("BlazingBooster")) {
				A_GiveInventory("NicotineToken",5);
			}
			//Alchemist's Potency bonus.
			If(CountInv("AlchemistsPotency")) {
				A_GiveInventory("MegasphereBoost");
			}
			//Stuff.
			A_SpawnItemEx("CigbuttHail",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			A_SpawnItemEx("MegasphereEffect",0,0,0,0,0,0,0,SXF_SETTARGET);
			A_GiveInventory("MegasphereArmor2",1);
			A_GiveInventory("MegasphereHealth2",1);
			A_GiveInventory("NicotineToken",15);
			
			//Nicotine Rush.
			If(CountInv("NicotineToken") >= 100) {
				A_TakeInventory("NicotineAchtung1",1);
				A_TakeInventory("NicotineAchtung2",1);
				A_TakeInventory("NicotineAchtung3",1);
				A_TakeInventory("NicotineToken",100);
				A_GiveInventory("NicotineRushItem");
			}
			Return State("");
		}	
		Stop
	}
}

Actor CigbuttHail
{
	Var Int User_ButtCount;
	+NOINTERACTION
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
		for (A_SetUserVar(User_ButtCount, 0); User_ButtCount < 20; A_SetUserVar(User_ButtCount, User_ButtCount + 1))
			{
				A_SpawnItemEx("Cigbutt",
				frandom(5.0,-5.0),frandom(5.0,-5.0),20+frandom(5.0,-5.0),
				frandom(1.0,5.0),0,frandom(2.0,6.0),
				frandom(0.0,360.0),SXF_CLIENTSIDE,48);
			}
		}
		Stop
	}
}

Actor MegasphereArmor2 : BodyArmor
{
}

Actor MegasphereHealth2 : MegasphereHealth
{
	Inventory.MaxAmount 999
}

//Potency upgrade
Actor MegasphereBoost : MegasphereHealth2
{
	Inventory.Amount 25
}

//Stored powerup
ACTOR MegaspherePortable : TBPowerupPortableBase
{
	Inventory.PickupMessage "$GOTMEGASPHERE"
	Inventory.Icon ARTITBMS
	Inventory.PickupSound "Megasphere/Pickup"
	Tag "Cigsphere"
	States
	{
	Spawn:
		TBMS AAAABBBBCCCCDDDDEEEEFFFFEEEEDDDDCCCCBBBB 1 Bright {
			If(!(A_CheckSight("Null") || GetCVar("TB_ParticleReduction")))
			{
				If(A_JumpIf(User_Count % 4 == 0, "Null"))
				{
					A_SpawnItemEx("MegaFade",0,0,0,0,0,0,0,SXF_CLIENTSIDE,0);
					A_SetUserVar(User_Count, 0);
				}
				A_SetUserVar(User_Count, User_Count + 1);
				A_SpawnItemEx("MegasphereParticle",
				frandom(10.0,-10.0),frandom(10.0,-10.0),35+frandom(10.0,-10.0),
				frandom(0.6,-0.6),frandom(0.6,-0.6),frandom(1.2,0.4),
				frandom(0.0,360.0),SXF_CLIENTSIDE,64);
			}
		}
		Loop
	Use:
		TNT1 A 0 {
			//Blazing Booster bonus.
			If(CountInv("BlazingBooster")) {
				A_GiveInventory("NicotineToken",5);
			}
			//Stuff.
			A_PlaySound("Megasphere/Pickup",3);
			A_SpawnItemEx("CigbuttHail",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			A_SpawnItemEx("MegasphereEffect",0,0,0,0,0,0,0,SXF_SETTARGET);
			A_GiveInventory("MegasphereArmor2",1);
			A_GiveInventory("MegasphereHealth2",1);
			A_GiveInventory("NicotineToken",15);
			
			//Nicotine Rush.
			If(CountInv("NicotineToken") >= 100) {
				A_SetBlend("Teal",0.3,70);
				A_TakeInventory("NicotineToken",100);
				A_GiveInventory("NicotineRushItem");
			}
			//Alchemist's Potency bonus.
			If(CountInv("AlchemistsPotency")) {
				A_GiveInventory("MegasphereBoost");
			}
		}
		Stop
	} 
}

//Eyecandy
Actor MegaFade : TBFadeBase
{
	States
	{
	Spawn:
		TBMS C 0
		Goto Super::Fade
	}
}

ACTOR MegasphereParticle : TBItemParticle
{
	States
	{
	Spawn:
		KPRT A 0
	SetScale:
		"####" "#" 0 A_SetScale(0.1)
	Main:
		"####" "#" 0 A_JumpIf(ScaleX <= 0,"NULL")
		"####" "#" 0 A_FadeOut(frandom(0.02,0.04))
		"####" "#" 0 A_ChangeVelocity(0,0,frandom(-0.1,0.15),CVF_RELATIVE)
		"####" "#" 1 Bright A_SetScale(ScaleX-frandom(-0.01,0.02))
		Loop
	}
}

Actor MegasphereEffect : TBPowerupEffect
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			for (A_SetUserVar(User_Counter, 0); User_Counter < 6; A_SetUserVar(User_Counter, User_Counter + 1))
			{
				A_SetAngle(frandom(0.0,360.0));
				A_SpawnItemEx("MegaspherePart",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPOINTERS);
			}
		}
		TNT1 A 0 A_SetUserVar("User_Counter", 40)
		Goto Super::Loop2
	}
}

Actor MegaspherePart : TBPowerupPart
{
	States
	{
	Spawn:
		PSHK A 0
		Goto Super::Setup
	}
}
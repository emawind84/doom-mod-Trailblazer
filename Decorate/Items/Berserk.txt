//Berserk
ACTOR Berserk2 : Berserk replaces Berserk
{
	Inventory.PickupMessage "$GOTBERSERK"
	Inventory.PickupSound "Berserk/Pickup"
	States
	{
	Spawn:
		PSTR A -1
		Stop
	Pickup:
		TNT1 A 0 {
			if(CountInv("AlchemistsDream") && (CountInv("BerserkPortable") < 3) && GetPlayerInput(INPUT_BUTTONS) & BT_USE)
			{
				A_Print("$BERSERKSTORED");
				A_GiveInventory("BerserkPortable",1);
				Return State("Null");
			}
			A_GiveInventory("StrengthIcon");
			A_SpawnItemEx("BerserkEffect",0,0,0,0,0,0,0,SXF_SETTARGET);
			HealThing(100, 0);
			If(!CountInv("PowerStrength2"))
			{
				A_SetMugshotState("Grin");
				A_SelectWeapon("Macheterang");
			}
			A_GiveInventory("PowerStrength2");
			Return State("");
		}
		Stop
	}
}

//Portable
ACTOR BerserkPortable : TBPowerupPortableBase
{
	Inventory.PickupMessage "$GOTBERSERK"
	Inventory.Icon "ARTIPSTR"
	Inventory.PickupSound "Berserk/Pickup"
	Tag "Berserk"
	Renderstyle Normal
	-FLOATBOB
	States
	{
	Spawn:
		PSTR A -1
		Stop
	Use:
		TNT1 A 0 {
			A_SetBlend("Red",0.3,70);
			A_PlaySound("Berserk/Pickup",3);
			A_GiveInventory("StrengthIcon");
			A_SpawnItemEx("BerserkEffect",0,0,0,0,0,0,0,SXF_SETTARGET);
			HealThing(100, 0);
			If(A_JumpIf(CountInv("PowerStrength2") > 0, "Null"))
			{
				A_SetMugshotState("Grin");
				A_SelectWeapon("Macheterang");
			}
			A_GiveInventory("PowerStrength2");
		}
		Stop
	} 
}

//Eyecandy
Actor BerserkEffect : TBPowerupEffect
{
	States
	{
	Spawn:
		TNT1 A 0 NoDelay
		{
			for (A_SetUserVar(User_Counter, 0); User_Counter < 6; A_SetUserVar(User_Counter, User_Counter + 1))
			{
				A_SetAngle(frandom(0.0,360.0));
				A_SpawnItemEx("BerserkPart",0,0,0,0,0,0,0,SXF_CLIENTSIDE|SXF_TRANSFERPOINTERS);
			}
		}
		TNT1 A 0 A_SetUserVar("User_Counter", 40)
		Goto Super::Loop2
	}
}

Actor BerserkPart : TBPowerupPart
{
	States
	{
	Spawn:
		NTCT B 0
		Goto Super::Setup
	}
}

ACTOR PowerStrength2 : PowerStrength
{
	-INVENTORY.HUBPOWER
}

actor StrengthIcon : PowerupGiver
{
	Inventory.Amount 0
	powerup.type "StrengthFake"
	powerup.duration -0x7FFFFFFF
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	states
	{
	Spawn:
		TNT1 A 1
		stop
	}
}

actor PowerStrengthFake : PowerDamage
{	
	Inventory.Icon "BRZERK0"
	DamageFactor "Normal", 1.0
}
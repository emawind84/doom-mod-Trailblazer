ACTOR Flarebox : CustomInventory
{
	Inventory.Icon "ARTIFBOX"
	Inventory.PickupMessage "Got a box of... light cigars?"
	Inventory.MaxAmount 25
	Inventory.Amount 5
	Inventory.InterHubAmount 25
	Inventory.PickupSound "Cigbox/Pickup"
	Inventory.UseSound ""
	Tag "Shine on you crazy diamond"
	Scale 0.8
	+INVBAR
	+INVENTORY.UNDROPPABLE
	States
	{
	Spawn:
		FBOX A -1
		Stop
	Use:
		//TNT1 A 0 A_JumpIfInventory("PowerFlareActive",1,"Nope")
		TNT1 A 0 {
			A_PlaySound("Flare/Use",3);
			if(GetPlayerInput(INPUT_BUTTONS) & BT_USE) {
				If(CountInv("PowerFlareActive") <= 0)
				{
					A_SpawnItemEx("Flaresticker",0,0,0,0,0,0,0,SXF_SETTARGET);
				}
				A_GiveInventory("FlareTimer");
			} else {
				A_FireCustomMissile("Flarestick",0,0,0,0,FPF_NOAUTOAIM);
			}
		}
		Stop
	Nope:
		TNT1 A 0
		Fail
	}
}

actor FlareTimer : PowerupGiver
{
  Inventory.Amount 0
  powerup.type "FlareActive"
  powerup.duration -120
  +INVENTORY.AUTOACTIVATE
  +INVENTORY.ALWAYSPICKUP
  +INVENTORY.ADDITIVETIME
  states
  {
  Spawn:
		TNT1 A 1
		stop
  }
}

actor PowerFlareActive : PowerDamage
{	
	Inventory.Icon "FLRAKTV0"
	DamageFactor "Normal", 1.0
}

actor Flaresticker
{
	Projectile
	+NOINTERACTION
	+NOTIMEFREEZE
	States
	{
	Spawn:
		TNT1 A 1 NoDelay {
			A_SetBlend("Orange",0.1,10);
			A_PlaySound("Flare/Loop",5,0.2,1);
			A_Warp(AAPTR_TARGET,0,0,0,0,WARPF_NOCHECKPOSITION|WARPF_INTERPOLATE,0,0,0);
		}
		TNT1 A 0 A_JumpIf(CountInv("PowerFlareActive",AAPTR_TARGET), "Spawn")
	End:
		TNT1 A 0 A_StopSound(5)
		Stop
	}
}

actor Flarestick
{
	Radius 4
	Height 6
	Speed 25
	+ROLLSPRITE
	+ROLLCENTER
	+MBFBOUNCER
	+INTERPOLATEANGLES
	var int user_timer;
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			A_ChangeVelocity(0,0,4);
			A_PlaySound("Flare/Loop",5,0.2,1);
			user_timer = 4200; // 35 * 120 tics.
		}
	PainisInTheAinis:
		FSTK AAABBB 1 Bright {
			if(z == floorz) { a_setroll(0); }
			else { a_setroll(roll + 15.0, SPF_INTERPOLATE); }
			user_timer--;
			Return A_JumpIf(user_timer <= 0, "Ded");
		}
		Loop
	Ded:
		FSTK C 0 {
			A_SpawnItemEx("FlarestickLeftover",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			A_StopSound(5);
		}
		Stop
	}
}

actor FlarestickLeftover : Flarestick
{
	States
	{
	Spawn:
		FSTK C 210 A_CheckSight("Null")
		Loop
	}
}
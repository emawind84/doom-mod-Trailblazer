//Health
Actor Stimpack2 : Stimpack replaces Stimpack
{
	Inventory.PickupSound "Stimpack/Pickup"
}

Actor Medikit2 : Medikit replaces Medikit
{
	Inventory.PickupSound "Medikit/Pickup"
}

Actor MedikitRaven : HealthPickup
{
	Tag "Shrink pack"
	Inventory.Amount 1
	Inventory.MaxAmount 25
	Inventory.InterHubAmount 0x7FFFFFFF
	Inventory.PickupSound "Stimpack/Pickup"
	Inventory.UseSound "Medikit/Pickup"
	Inventory.PickupMessage "Picked up a medikit."
	Inventory.Icon "ARTIMEDI"
	HealthPickup.Autouse 1
	Health 25
	+INVENTORY.INVBAR
	States
	{
	Spawn:
		MEDI A -1
		Stop
	} 
}

Actor Marlo : HealthBonus
{
	Inventory.PickupMessage "Nabbed a pack of marloes."
	Inventory.PickupSound "Cigs/Pickup"
	Inventory.Amount 1
	Inventory.MaxAmount 999
	States
	{
	Spawn:
		BON1 A -1
		Stop
	}
}

Actor NicotineAchtung1 : Inventory { Inventory.MaxAmount 1 }
Actor NicotineAchtung2 : NicotineAchtung1 {}
Actor NicotineAchtung3 : NicotineAchtung1 {}

Actor MarloPickup : CustomInventory
{
	Inventory.MaxAmount 0
	Inventory.PickupSound "Cigs/Pickup"
	Inventory.PickupMessage "Nabbed a pack of marloes."
	+INVENTORY.AUTOACTIVATE
	+INVENTORY.ALWAYSPICKUP
	//+INVENTORY.UNDROPPABLE
	States
	{
	Spawn:
		BON1 A -1
      Stop
	Pickup:
		TNT1 A 0 {
			A_GiveInventory("NicotineToken",1);
			A_GiveInventory("Marlo",2);
			
			//Nicotine rush warnings.
			If(CountInv("NicotineToken") >= 75 && !CountInv("NicotineAchtung1")) { A_Print("\c[white]about 25 cigs to nicotine rush."); A_GiveInventory("NicotineAchtung1",1); }
			If(CountInv("NicotineToken") >= 90 && !CountInv("NicotineAchtung2")) { A_Print("\c[white]about 10 cigs to nicotine rush."); A_GiveInventory("NicotineAchtung2",1); }
			If(CountInv("NicotineToken") >= 95 && !CountInv("NicotineAchtung3")) { A_Print("\c[gold]about 5 cigs to nicotine rush!");   A_GiveInventory("NicotineAchtung3",1); }
			
			//Blazing Booster bonus.
			If(CountInv("BlazingBooster") && random(1,5) == 1) {
				A_GiveInventory("NicotineToken",1);
			}
		}
		TNT1 A 0 
		TNT1 A 0 A_JumpIfInventory("LuckyStrike",1,"SpinTheWheel")
		Goto CheckNicotine
	SpinTheWheel:
		TNT1 A 0 { if(A_Jump(64,"Null")) { A_GiveInventory("Marlo",random(1,2)); } }
		TNT1 A 0 A_Jump(6,"YoureWiener")
		Goto CheckNicotine
	YoureWiener:
		TNT1 A 0 {
			A_GiveInventory("LuckyStrikeItem");
			A_GiveInventory("Marlo",100);
			if(A_JumpIf(CountInv("AlchemistsPotency") > 0, "Null")) {
				A_GiveInventory("Marlo",25);
			}
		}
		Goto CheckNicotine
	CheckNicotine:
		TNT1 A 0 A_JumpIf(CountInv("NicotineToken") >= 100, "NicotineRush")
		Stop
	NicotineRush:
		TNT1 A 0 {
			A_TakeInventory("NicotineAchtung1",1);
			A_TakeInventory("NicotineAchtung2",1);
			A_TakeInventory("NicotineAchtung3",1);
			A_TakeInventory("NicotineToken",100);
			A_GiveInventory("NicotineRushItem");
		}
		Stop
	}
}

Actor HealthBonus2 : CustomInventory
{
    Inventory.Pickupmessage "Picked up a health bonus."
	Inventory.PickupSound "Syringe/Boost"
    Inventory.Amount 1
    Inventory.MaxAmount 100
    +INVENTORY.ALWAYSPICKUP
	+INVENTORY.UNDROPPABLE
	+FLOATBOB
    //+COUNTITEM
    states
	{
	Spawn:
		BON1 B -1
		Stop
	Pickup:
		TNT1 A 0 {
			if(!(GetSpawnHealth>=200)) {
				ACS_NamedExecuteAlways("Marichuanen z Tuczonym Szkuem");
				A_GiveInventory("HealthExpander",1);
				A_GiveInventory("Marlo");
			} Else {
				A_GiveInventory("Marlo");
			}
		}
		Stop
	}
}

Actor HealthExpander : Inventory { Inventory.MaxAmount 100 }
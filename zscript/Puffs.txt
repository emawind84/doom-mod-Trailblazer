class EliminatorPuff : Actor
{
	Default
	{
		ProjectileKickback 125; //300
		Decal "BulletChip";
		Species "Chainsmoker";
		+MTHRUSPECIES
		+NOGRAVITY
		+NOBLOCKMAP
		+NOINTERACTION
	}
	
	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			for(int fuck = 0; fuck < 3; fuck++) { A_SpawnItemEx("BuellerPuff_Main", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE); }
			for(int shit = 0; shit < 2; shit++) { A_SpawnItemEx("BuellerPuff_Flash", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE); }
			A_SpawnItemEx("BuellerPuff_Spark1", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE);
			A_SpawnItemEx("BuellerPuff_Spark2", 0, 0, 0, 0, 0, 0, 0, SXF_CLIENTSIDE);
		}
		TNT1 AAA 1 A_SpawnItemEx("BuellerPuff_Smoke", 0, 0, 0, frandom(0.15,0.25), 0, frandom(-0.1,0.45), 0, SXF_CLIENTSIDE);
		Stop;
	}
}


class EliminatorUraniumPuff : EliminatorPuff
{
	Default
	{
		Decal "NutcrackerMinorChip";
		+ALWAYSPUFF
	}

	States
	{
	Spawn:
		TNT1 A 0 NoDelay {
			A_PlaySound("Uranium/Hit",0,0.25);
			A_Explode(18,32,0);
			A_QuakeEx(1,1,1,10,0,52,"none",QF_SCALEDOWN);
		}
	Rest:
		TNT1 A 0 {
			A_SpawnItemEx("UraniumPuff_Blast",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			A_SpawnItemEx("UraniumPuff_Flare",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
			A_SpawnItemEx("UraniumPuff_Shockwave",0,0,0,0,0,0,0,SXF_CLIENTSIDE);
		}
		TNT1 AAA 1 {
			A_SpawnItemEx("UraniumPuff_Smoke",0,0,0,frandom(0.15,0.25),0,frandom(-0.1,0.45),0,SXF_CLIENTSIDE,48);
			A_SpawnItemEx("UraniumPuff_Cloud", frandom(-3.0, 3.0),frandom(-3.0, 3.0), frandom(-3.0, 3.0),
											   frandom(-0.8, 0.8), frandom(-0.8, 0.8),frandom(-0.8, 0.8),
											   frandom(0.0,360.0), SXF_CLIENTSIDE, 48);				   
			A_SpawnItemEx("UraniumPuff_Ember", 0,0,0,frandom(0.2, 1.5),0,frandom(0.2, 1.5), frandom(0.0,360.0),SXF_CLIENTSIDE,48);
			A_SpawnItemEx("UraniumPuff_Ember", 0,0,0,frandom(0.2, 1.5),0,frandom(0.2, 1.5), frandom(0.0,360.0),SXF_CLIENTSIDE,48);
		}
		TNT1 A 10;
		Stop;
	Crash:
		TNT1 A 0 { bPUFFONACTORS = 1; }
		Goto Spawn;
	}
}

class EliminatorPuffRail : EliminatorPuff
{
	Default
	{
		+PUFFONACTORS
		+PUFFGETSOWNER
		+ALWAYSPUFF
		+FORCEDECAL
	}
	
	States
	{
	Crash:
		TNT1 A 0 { bPUFFONACTORS = 1; }
		Goto Super::Spawn;
	XDeath:
		TNT1 A 0;
		Stop;
	}
}

class EliminatorUraniumPuffRail : EliminatorUraniumPuff
{
	Default
	{
		+PUFFONACTORS
		+PUFFGETSOWNER
		+ALWAYSPUFF
		+FORCEDECAL
	}

	States
	{
	Spawn:
		TNT1 A 1 NoDelay {
			//if(Target) { LineAttack(0, 8, target.pitch, 0, "Normal", "NukellerPuff"); }
			A_PlaySound("Uranium/Hit",0,0.35);
			A_Explode(32,32,0);
			A_QuakeEx(1,1,1,10,0,52,"none",QF_SCALEDOWN);
		}
		Goto Super::Rest;
	Crash:
		TNT1 A 0 { bPUFFONACTORS = 1; }
		Goto Spawn;
	}
}